****************************************************
* PROGRAM         : Generate Debit Interest Post Report
* DEV BY      : MD. IMRAN HASAN
* DEV DATE    : 2016-12-25
* UPDATE DATE : 2016-12-25
* VERSION 3.0: 20200328 | ALIN BOBY | INTT SUSP AC LIST GEN | REQ: CORE CELL
* REQ         : ICTD
* Tools *******
!TSM SERVICE - BNK/OFS.MESSAGE.SERVICE
!TSM SERVICE - BNK/OFS.RESPONSE.QUEUE
!ENQUIRY.REPORT,ACCT.DR.INT I ER.STMT.ACCT.DR
****************************************************
! SUBROUTINE R.ICTD.RG.DEBIT.INT.POST
    PROGRAM R.ICTD.RG.DEBIT.INT.POST
    $INSERT GLOBUS.BP I_COMMON
    $INSERT GLOBUS.BP I_EQUATE
    $INSERT GLOBUS.BP I_F.COMPANY
    $INSERT GLOBUS.BP I_F.COMPANY.SMS.GROUP


    FN.COM.SMS.GROUP='F.COMPANY.SMS.GROUP'
    F.COM.SMS.GROUP=''

    CALL OPF(FN.COM.SMS.GROUP,F.COM.SMS.GROUP)


    Y.FILE.DIR = 'JBL.BP/REPGEN'
    Y.FILE.NAME = 'PARAMETER.DR.INT.txt'
    Y.FILE = Y.FILE.DIR :'/': Y.FILE.NAME


    OPENSEQ Y.FILE TO F.FILE.POINT THEN
        LOOP
            READSEQ Y.RECORD FROM F.FILE.POINT ELSE
                Y.RECORD = ''
            END
        WHILE Y.RECORD
            Y.REC<-1> = Y.RECORD
        REPEAT
    END

    Y.TOT1=DCOUNT(Y.REC,@FM)
    FOR K = 1 TO Y.TOT1
        L.VALUES = FIELD(Y.REC,@FM,K)
        COMP.ID = FIELD(L.VALUES,',',1)
        Y.CATEG.ID = FIELD(L.VALUES,',',2)

        Y.V.MESSAGE = "ENQUIRY.REPORT,ACCT.DR.INT/V/PROCESS,//":COMP.ID:",ER.STMT.ACCT.DR.":Y.CATEG.ID
        V.MSG.ID = ''
        CALL OFS.POST.MESSAGE(Y.V.MESSAGE, V.MSG.ID ,"DM.OFS.SRC.VAL", "DMUSER")
        CALL JOURNAL.UPDATE ('TEST')

    NEXT


* GENERATE INTT SUSP AC LIST REPORT
*--------------------------------------------

    CALL F.READ(FN.COM.SMS.GROUP,'INTRBRCOMP',R.SMS,F.COM.SMS.GROUP,AC.ERR)
    Y.COM.CODE=R.SMS<CO.SMS.COMPANY.CODE>
    Y.TOT=DCOUNT(Y.COM.CODE,@VM)
    FOR I =1  TO Y.TOT
        Y.ARR.COMP=FIELD(Y.COM.CODE,@VM,I)
        Y.V.MESSAGE1 = "ENQUIRY.REPORT,ACC.SUSP.LIST/V/PROCESS,//":Y.ARR.COMP:",ER.ACC.SUSP.LIST"
        V.MSG.ID1 = ''
        CALL OFS.POST.MESSAGE(Y.V.MESSAGE1, V.MSG.ID1 ,"DM.OFS.SRC.VAL", "DMUSER")
        CALL JOURNAL.UPDATE ('TEST')
    NEXT I

    CRT "TOTAL RECORDS FOR DEBIT INTT POSTED : ":Y.TOT1
    CRT "TOTAL RECORDS FOR INTEREST SUSPENSE ACCOUNT LIST : ":Y.TOT
    Y.TOTAL.REC = Y.TOT1 + Y.TOT
    CRT "TOTAL RECORDS TO BE PROCESSED FOR REPORT GENERATION : ":Y.TOTAL.REC


    RETURN
END
