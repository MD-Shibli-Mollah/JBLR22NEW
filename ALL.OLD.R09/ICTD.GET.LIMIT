*-----------------------------------------------------------------------------
* <Rating>69</Rating>
*-----------------------------------------------------------------------------
* IFNO
* PROGRAM     : PROGRAM TO CREATE A CSV FILE FOR LIMIT
* DEV BY      : MD. IMRAN HASAN
* DEV DATE    : 2021-01-04
* UPDATE DATE : 2021-01-04
* REQ         : ICTD
****************************************************

    PROGRAM ICTD.GET.LIMIT
    $INSERT GLOBUS.BP I_COMMON
    $INSERT GLOBUS.BP I_EQUATE
    $INSERT GLOBUS.BP I_F.LIMIT


    GOSUB INIT
    GOSUB OPENFILES
    GOSUB PROCESS


*------
INIT:
*------

    Y.FILE.NAME = 'MT.LIMIT.DATA.':TODAY:'.csv'
    Y.FILE.DIR = 'RPT.DATA.DIR'
    Y.FILE.PATH = Y.FILE.DIR:'/':Y.FILE.NAME

    FN.LI = 'FBNK.LIMIT'
    F.LI = ''

    RETURN

*---------
OPENFILES:
*---------
    CALL OPF(FN.LI,F.LI)

    CALL GET.LOC.REF("LIMIT","SUB.PARENT",Y.SUB.PARENT.POS)
    CALL GET.LOC.REF("LIMIT","CHILD.LIMIT",Y.CHILD.LIMIT.POS)
    CALL GET.LOC.REF("LIMIT","RENEW.REF.NO",Y.RENEW.REF.NO.POS)
    CALL GET.LOC.REF("LIMIT","RENEW.REF.DATE",Y.RENEW.REF.DATE.POS)
    CALL GET.LOC.REF("LIMIT","NO.OF.RENEW",Y.NO.OF.RENEW.POS)
    CALL GET.LOC.REF("LIMIT","NO.OF.ENHANCE",Y.NO.OF.ENHANCE.POS)
    CALL GET.LOC.REF("LIMIT","SANCTION.AUTHORITY",Y.SANCTION.AUTHORITY.POS)

!-------Check Directory----------
    CMD.STR = "CREATE.FILE RPT.DATA.DIR TYPE=UD"
    CUR.DIR = "RPT.DATA.DIR"

    OPEN CUR.DIR TO F.RPT.DATA.DIR
    ELSE
        EXECUTE CMD.STR
        OPEN CUR.DIR TO F.RPT.DATA.DIR
        ELSE
            CRT CUR.DIR "OPENING FAILED"
            RETURN
        END
    END
!-------------------------------

    RETURN

*-------
PROCESS:
*-------

    SEL.CMD='SELECT ':FN.LI
    CALL EB.READLIST(SEL.CMD,SEL.LIST,'',NO.OF.REC,RET.CODE)


    LOOP
        REMOVE Y.REC.ID FROM SEL.LIST SETTING Y.POS
    WHILE Y.REC.ID:Y.POS
        CALL F.READ(FN.LI,Y.REC.ID,R.LI,F.LI,Y.ERR)


        Y.LIMIT.CURRENCY   = R.LI<LI.LIMIT.CURRENCY>
        Y.APPROVAL.DATE    = R.LI<LI.APPROVAL.DATE>
        Y.EXPIRY.DATE      = R.LI<LI.EXPIRY.DATE>

        Y.COLLATERAL.CODE  = R.LI<LI.COLLATERAL.CODE>
        Y.INTERNAL.AMOUNT  = R.LI<LI.INTERNAL.AMOUNT>
        Y.ONLINE.LIM.DATE  = R.LI<LI.ONLINE.LIMIT.DATE>

        Y.PROPOSAL.DATE    = R.LI<LI.PROPOSAL.DATE>
        Y.MAXIMUM.SECURE   = R.LI<LI.MAXIMUM.SECURED>
        Y.ONLINE.LIMIT     = R.LI<LI.ONLINE.LIMIT>

        Y.AVAIL.AMT        = R.LI<LI.AVAIL.AMT>
        Y.ORIGINAL.LIMIT   = R.LI<LI.ORIGINAL.LIMIT>
        Y.LIMIT.PRODUCT    = R.LI<LI.LIMIT.PRODUCT>
        Y.SANCTION.AUTH    = R.LI<LI.LOCAL.REF,Y.SANCTION.AUTHORITY.POS>

        Y.SUB.PARENT       = R.LI<LI.LOCAL.REF,Y.SUB.PARENT.POS>
        Y.CHILD.LIMIT      = R.LI<LI.LOCAL.REF,Y.CHILD.LIMIT.POS>
        Y.RENEW.REF.NO     = R.LI<LI.LOCAL.REF,Y.RENEW.REF.NO.POS>
        Y.NO.RENEW         = R.LI<LI.LOCAL.REF,Y.NO.OF.RENEW.POS>
        Y.NO.OF.ENHANCE    = R.LI<LI.LOCAL.REF,Y.NO.OF.ENHANCE.POS>
        Y.RENEW.REF.DATE   = R.LI<LI.LOCAL.REF,Y.RENEW.REF.DATE.POS>


        Y.REC<-1> = Y.REC.ID:'|':Y.LIMIT.CURRENCY:'|':Y.APPROVAL.DATE:'|':Y.EXPIRY.DATE:'|':Y.COLLATERAL.CODE:'|':Y.INTERNAL.AMOUNT:'|':Y.ONLINE.LIM.DATE:'|':Y.PROPOSAL.DATE:'|':Y.MAXIMUM.SECURE:'|':Y.ONLINE.LIMIT:'|':Y.AVAIL.AMT:'|':Y.ORIGINAL.LIMIT:'|':Y.LIMIT.PRODUCT:'|':Y.SANCTION.AUTH:'|':Y.SUB.PARENT:'|':Y.CHILD.LIMIT:'|':Y.RENEW.REF.NO:'|':Y.NO.RENEW:'|':Y.NO.OF.ENHANCE:'|':Y.RENEW.REF.DATE:'|':TODAY:'|'

        Y.COUNT++
        Y.PROGRESS = MOD(Y.COUNT,5000)
        IF Y.PROGRESS EQ 0 THEN
            Y.PERCENT = Y.COUNT*100/NO.OF.REC
            PRINT DROUND(Y.PERCENT,0):'% is Completed.'
        END

    REPEAT

    OPEN Y.FILE.DIR TO F.FILE.DIR ELSE NULL
    WRITE Y.REC TO F.FILE.DIR,Y.FILE.NAME

    RETURN
END
