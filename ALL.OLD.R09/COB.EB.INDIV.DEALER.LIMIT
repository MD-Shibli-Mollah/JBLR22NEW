*
*-----------------------------------------------------------------------------
* <Rating>-11</Rating>
*-----------------------------------------------------------------------------
    SUBROUTINE COB.EB.INDIV.DEALER.LIMIT(DEALER.ID)

    $INSERT T24.BP I_COMMON
    $INSERT T24.BP I_EQUATE
    $INSERT T24.BP I_F.FOREX
    $INSERT I_F.INDIV.DEALER.LIMIT
    $INSERT I_F.OVE.DEALING.LIMIT
    $INSERT I_COB.EB.INDIV.DEALER.LIMIT.COMMON

    DEAL.ROOM.ID=DEALER.ID[1,2]

    CALL F.READ(FN.INDIV.DEALER.LIMIT,DEALER.ID,R.INDIV.DEALER.LIMIT.REC,F.INDIV.DEALER.LIMIT,Y.DEALER.LIMIT.ERR)

    IF R.INDIV.DEALER.LIMIT.REC<EB.IND48.APPLICATION> EQ 'FX' THEN
        GOSUB FX.PROCESS
    END
    R.INDIV.DEALER.LIMIT.REC<EB.IND48.PER.DAY.AVAIL.AMT> = R.INDIV.DEALER.LIMIT.REC<EB.IND48.PER.DAY.LIMIT.AMT>
    R.INDIV.DEALER.LIMIT.REC<EB.IND48.PER.DAY.UTIL.AMT> = ''

    CALL F.READ(FN.OVE.DEALING.LIMIT,DEAL.ROOM.ID,R.DEAL.LIMIT.REC,F.OVE.DEALING.LIMIT,Y.DEAL.LIMIT.ERR)

    R.DEAL.LIMIT.REC<EB.OVE45.OVE.AVAIL.AMOUNT> = R.DEAL.LIMIT.REC<EB.OVE45.OVE.LI.AMOUNT>
    R.DEAL.LIMIT.REC<EB.OVE45.OVE.UTIL.AMOUNT> = ''
    R.DEAL.LIMIT.REC<EB.OVE45.LCY.AVAIL.AMOUNT> = R.DEAL.LIMIT.REC<EB.OVE45.LCY.OVERALL.LIMIT>
    R.DEAL.LIMIT.REC<EB.OVE45.LCY.UTIL.AMOUNT> = ''

    CALL F.WRITE(FN.INDIV.DEALER.LIMIT,DEALER.ID,R.INDIV.DEALER.LIMIT.REC)
    CALL F.WRITE(FN.OVE.DEALING.LIMIT,DEAL.ROOM.ID,R.DEAL.LIMIT.REC)

    RETURN

FX.PROCESS:

    SEL.CMD.FX="SELECT ": FN.FOREX :" WITH DEALER.NO EQ ": DEALER.ID :" AND DEAL.DATE EQ ": TODAY
    CALL EB.READLIST(SEL.CMD.FX,FX.LIST.ID,'',NO.OF.REC,RET.CODE)
    LOOP
        REMOVE FX.ID FROM FX.LIST.ID SETTING Y.POS
    WHILE FX.ID:Y.POS
        CALL F.READ(FN.FOREX,FX.ID,R.FX.REC,F.FOREX,Y.FX.ERR)
        ARR.CCY = R.INDIV.DEALER.LIMIT.REC<EB.IND48.PER.DEAL.LI.CCY>
        CURR.BOUGHT=R.FX.REC<FX.CURRENCY.BOUGHT>
        LOCATE CURR.BOUGHT IN ARR.CCY<1,1> SETTING CUR.POS THEN
            R.INDIV.DEALER.LIMIT.REC<EB.IND48.OVER.NIGHT.AMT,CUR.POS> += R.FX.REC<FX.AMOUNT.BOUGHT>
        END
        CURR.SOLD=R.FX.REC<FX.CURRENCY.SOLD>
        LOCATE CURR.SOLD IN ARR.CCY<1,1> SETTING CUR.POS1 THEN
            R.INDIV.DEALER.LIMIT.REC<EB.IND48.OVER.NIGHT.AMT,CUR.POS1> += -(R.FX.REC<FX.AMOUNT.SOLD>)
        END

    REPEAT
    RETURN
