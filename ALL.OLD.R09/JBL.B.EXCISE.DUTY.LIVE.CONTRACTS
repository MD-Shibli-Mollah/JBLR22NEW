*-----------------------------------------------------------------------------
* <Rating>-177</Rating>
*-----------------------------------------------------------------------------
    SUBROUTINE JBL.B.EXCISE.DUTY.LIVE.CONTRACTS(Y.LD.ID)
**************************************************************************
*Subroutine Description:
*-----------------------
* This routine is used to detect and apply excise duty on all the
* live LDs and PDPD
*-------------------------------------------------------------------------
* Project Name : Janata Bank(Bangladesh)
*-------------------------------------------------------------------------
*Parameter Description
*--------- -----------
*  NA
*-------------------------------------------------------------------------
*Common Variable Description
*------  ------- -----------
*  NA
*-------------------------------------------------------------------------
*Called Subroutines:
*------ ------------
* Routine Name:             Local/Core:     Description:
*
* EXCISE.DUTY.MAIN.PROCESS     LOCAL        Detects and applies excise
*                                           duty on the selected LDs
*-------------------------------------------------------------------------
* Modification History:
* ------------ --------
* 03/02/2011 -                            New   - Rashmitha M / Leelavathi R
*                                                 Thesys Technologies
* 23/02/2011 -                         Amendemnt - Added PDPD Contracts
**************************************************************************
    $INCLUDE GLOBUS.BP I_COMMON
    $INCLUDE GLOBUS.BP I_EQUATE
    $INCLUDE JBL.BP I_JBL.B.EXCISE.DUTY.LIVE.CONTRACTS.COMMON
    $INCLUDE GLOBUS.BP I_F.LD.LOANS.AND.DEPOSITS
    $INCLUDE GLOBUS.BP I_F.LMM.ACCOUNT.BALANCES
    $INCLUDE GLOBUS.BP I_F.PD.PAYMENT.DUE
    $INCLUDE GLOBUS.BP I_F.ACCOUNT
    $INCLUDE GLOBUS.BP I_F.COMPANY
    $INCLUDE GLOBUS.BP I_F.FT.COMMISSION.TYPE
    $INCLUDE GLOBUS.BP I_F.TAX
    $INCLUDE GLOBUS.BP I_F.STMT.ENTRY
    $INCLUDE GLOBUS.BP I_F.CATEG.ENTRY
    $INCLUDE JBL.BP I_F.BD.EXCISE.DUTY.SLAB
    $INCLUDE JBL.BP I_F.EB.BD.EXCISE.DUTY.PARAM
*
    GOSUB INIT
    GOSUB PROCESS
*
    RETURN
*
******************************************************
INIT:
*****
! Initialise Variables
    Y.APPLY.CHARGE=''
*
    R.LD.LOANS.AND.DEPOSITS=''
    Y.ERR.LD=''
    R.PD.PAYMENT.DUE=''
    Y.ERR.PD=''
    R.LMM.ACCOUNT.BALANCES=''
    Y.ERR.AB=''
    R.ACCT.REC=''
    Y.ERR.AC=''
*
    AMT.FROM.POS='1'

    Y.SLAB.AMT='0'
    CO.POS=''
    Y.INTERNAL.ACCT.CAT=''
    Y.INTERNAL.ACCT=''
    Y.MAT.LEN=''
*
    Y.INT.LIQ.CCY=''
    Y.FCY.AMT=''
    Y.FCY.CUR=''
    Y.RATE= ''
    Y.MARKET=''
    Y.LCY.DR.AMT=''
    YDIF.AMT=''
    Y.DIF.RATE=''
    Y.FCY.DR.AMT=''
    Y.EXC.RATE=''
*
    RETURN
*****************************************************************************
PROCESS:
********
! Main Process
*
    BEGIN CASE
    CASE (Y.LD.ID[1,4] EQ 'PDPD')

        Y.PARAM.ID='PD'
        GOSUB READ.EB.BD.EXCISE.DUTY.PARAM

        Y.PD.ID = Y.LD.ID
        CALL F.READ(FN.PD.PAYMENT.DUE,Y.PD.ID,R.PD.PAYMENT.DUE,F.PD.PAYMENT.DUE,PD.ERR)
        IF R.PD.PAYMENT.DUE AND NOT(PD.ERR) THEN
            Y.CATEGORY = R.PD.PAYMENT.DUE<PD.CATEGORY>
            Y.PARAM.ID = 'PD'
            GOSUB READ.EB.BD.EXCISE.DUTY.PARAM
            GOSUB READ.LOAN.ACCOUNT
            GOSUB GET.INTERNAL.ACCT.CAT
            CALL EXCISE.DUTY.MAIN.PROCESS(R.EB.BD.EXCISE.DUTY.PARAM,Y.CATEGORY,Y.APPLY.CHARGE)
            GOSUB APPLY.EXCISE.DUTY.PD

        END
    CASE Y.LD.ID[1,2] EQ 'LD'

        Y.PARAM.ID='LD'
        GOSUB READ.EB.BD.EXCISE.DUTY.PARAM
        GOSUB READ.LD
        GOSUB GET.INTERNAL.ACCT.CAT
        CALL EXCISE.DUTY.MAIN.PROCESS(R.EB.BD.EXCISE.DUTY.PARAM,Y.CATEGORY,Y.APPLY.CHARGE)
        GOSUB APPLY.EXCISE.DUTY
    END CASE
*
    RETURN
****************************************************************************
READ.EB.BD.EXCISE.DUTY.PARAM:
****************************

    R.EB.BD.EXCISE.DUTY.PARAM='' ; Y.ERR.EBP = ''
    CALL F.READ(FN.EB.BD.EXCISE.DUTY.PARAM,Y.PARAM.ID,R.EB.BD.EXCISE.DUTY.PARAM,F.EB.BD.EXCISE.DUTY.PARAM,Y.ERR.EBP)
    IF NOT(R.EB.BD.EXCISE.DUTY.PARAM) THEN
        RETURN
    END
    Y.COMM.TYPE=R.EB.BD.EXCISE.DUTY.PARAM<EB.BD.30.COM.TYPE.EXCISE>

    CALL F.READ(FN.FT.COMMISSION.TYPE,Y.COMM.TYPE,R.FT.COMMISSION.TYPE,F.FT.COMMISSION.TYPE,FT.COMMISSION.TYPE.ERR)
    IF NOT(R.FT.COMMISSION.TYPE) THEN
        RETURN
    END
    RETURN

****************************************************************************
READ.LOAN.ACCOUNT:
******************

    Y.INT.LIQ.CCY = R.PD.PAYMENT.DUE<PD.LOCAL.REF,Y.LN.ACCT.POS>

    CALL F.READ(FN.ACCOUNT,Y.INT.LIQ.CCY,R.LOAN.ACCT,F.ACCOUNT,ACCOUNT.ERR)
    IF R.LOAN.ACCT AND NOT(ACCOUNT.ERR) THEN
        Y.CATEGORY = R.LOAN.ACCT<AC.CATEGORY>
        Y.LOAN.WORK.BAL = R.LOAN.ACCT<AC.WORKING.BALANCE>
    END

    RETURN
****************************************************************************
GET.INTERNAL.ACCT.CAT:
**********************
! Get the internal acct category for the company
    Y.CO=R.EB.BD.EXCISE.DUTY.PARAM<EB.BD.30.COMPANY>
    CONVERT VM TO FM IN Y.CO
    LOCATE ID.COMPANY IN Y.CO SETTING CO.POS THEN
        Y.INTERNAL.ACCT.CAT=R.EB.BD.EXCISE.DUTY.PARAM<EB.BD.30.INT.AC.CATEGORY,CO.POS>
    END ELSE
        Y.FIN.COMP=R.COMPANY(EB.COM.FINANCIAL.COM)
        LOCATE Y.FIN.COMP IN Y.CO SETTING CO.POS THEN
            Y.INTERNAL.ACCT.CAT=R.EB.BD.EXCISE.DUTY.PARAM<EB.BD.30.INT.AC.CATEGORY,CO.POS>
        END ELSE
            RETURN
        END
    END
*
    RETURN
*****************************************************************************
READ.LD:
********
! Read LD
    CALL F.READ(FN.LD.LOANS.AND.DEPOSITS,Y.LD.ID,R.LD.LOANS.AND.DEPOSITS,F.LD.LOANS.AND.DEPOSITS,Y.ERR.LD)
    IF NOT(R.LD.LOANS.AND.DEPOSITS) THEN
        RETURN
    END
    Y.CATEGORY=R.LD.LOANS.AND.DEPOSITS<LD.CATEGORY>
    Y.CURRENCY=R.LD.LOANS.AND.DEPOSITS<LD.CURRENCY>
    Y.VAL.DATE=R.LD.LOANS.AND.DEPOSITS<LD.VALUE.DATE>
    Y.MAT.DATE=R.LD.LOANS.AND.DEPOSITS<LD.FIN.MAT.DATE>
    Y.PRINC.AMT=R.LD.LOANS.AND.DEPOSITS<LD.AMOUNT>
    Y.INT.LIQ.ACCT=R.LD.LOANS.AND.DEPOSITS<LD.INT.LIQ.ACCT>
*
    RETURN
*******************************************************************************
APPLY.EXCISE.DUTY:
******************
! Apply excise duty on LD if it satisfies with the conditions given in BD.EXCISE.DUTY.PARAM
    IF Y.APPLY.CHARGE EQ 'Y' THEN
        GOSUB GET.OUTS.AMT
        GOSUB GET.DUTY.AMT
        GOSUB GEN.ENTRIES
    END

    RETURN
****************************************************************************
APPLY.EXCISE.DUTY.PD:
*********************

    IF Y.APPLY.CHARGE EQ 'Y' THEN
        Y.OUT.AMT = R.PD.PAYMENT.DUE<PD.TOTAL.AMT.TO.REPAY> + Y.LOAN.WORK.BAL
        Y.INT.LIQ.ACCT = R.PD.PAYMENT.DUE<PD.ORIG.STLMNT.ACT>
        GOSUB GET.DUTY.AMT
        GOSUB GEN.ENTRIES
    END
    RETURN

****************************************************************************
GET.OUTS.AMT:
*************
! Get the required loan outstanding amount on which the duty has to be applied.

    Y.PD.ID='PD':Y.LD.ID
    R.PD.PAYMENT.DUE='' ; Y.ERR.PD = ''
    CALL F.READ(FN.PD.PAYMENT.DUE ,Y.PD.ID ,R.PD.PAYMENT.DUE,F.PD.PAYMENT.DUE,Y.ERR.PD)
    IF R.PD.PAYMENT.DUE THEN
        Y.TOT.REPAY=R.PD.PAYMENT.DUE<PD.TOTAL.AMT.TO.REPAY>
        GOSUB GET.LMM.DETAILS
        Y.OUT.AMT= Y.OUT.PRINC+Y.TOT.REPAY
    END ELSE
        GOSUB GET.LMM.DETAILS
        Y.OUT.AMT = Y.OUT.PRINC+Y.OUT.INT
    END
    GOSUB CHECK.IF.FCY
*
    RETURN

*************************************************************************
GET.LMM.DETAILS:
****************
! Get the LMM.ACCOUNT.BALANCES details
    Y.LMM.ID=Y.LD.ID :'00'
    CALL F.READ(FN.LMM.ACCOUNT.BALANCES,Y.LMM.ID,R.LMM.ACCOUNT.BALANCES,F.LMM.ACCOUNT.BALANCES,LMM.ACCOUNT.BALANCES.ERR)
    Y.OUTS.CURR=R.LMM.ACCOUNT.BALANCES<LD27.OUTS.CURR.PRINC>
    Y.OUTS.COUNT=DCOUNT(Y.OUTS.CURR,VM)
    Y.OUT.PRINC=ABS(Y.OUTS.CURR<1,Y.OUTS.COUNT>)
    Y.OUT.INT=ABS(R.LMM.ACCOUNT.BALANCES<LD27.OUTS.ACCRUED.INT>)
*
    RETURN
*
************************************************************************
CHECK.IF.FCY:
*************
! If the loan is in Fcy convert the required outstanding amount to Lcy
    IF Y.CURRENCY NE LCCY THEN
        CALL EB.CURR.CONV(Y.CURRENCY,Y.OUT.AMT,LCCY,Y.LOC.AMT)
        Y.OUT.AMT=Y.LOC.AMT
    END
*
    RETURN
************************************************************************
GET.DUTY.AMT:
*************
! Get the excise duty amount for the calculated outstanding amount.

    Y.AMT=R.EB.BD.EXCISE.DUTY.SLAB<EB.BD.2.AMT.FROM>
    CONVERT VM TO FM IN Y.AMT
    Y.COUNT.AMT=DCOUNT(Y.AMT,FM)
    LOOP
    WHILE AMT.FROM.POS LE Y.COUNT.AMT
        Y.AMT.FRM=R.EB.BD.EXCISE.DUTY.SLAB<EB.BD.2.AMT.FROM,AMT.FROM.POS>
        Y.AMT.TO=R.EB.BD.EXCISE.DUTY.SLAB<EB.BD.2.AMT.TO,AMT.FROM.POS>
        IF Y.AMT.TO EQ '' AND  Y.OUT.AMT GE Y.AMT.FRM THEN
            Y.SLAB.AMT =R.EB.BD.EXCISE.DUTY.SLAB<EB.BD.2.EXCISE.DUTY.AMT,AMT.FROM.POS>
            EXIT
        END ELSE
            IF Y.OUT.AMT GE Y.AMT.FRM AND Y.OUT.AMT LE Y.AMT.TO THEN
                Y.SLAB.AMT =R.EB.BD.EXCISE.DUTY.SLAB<EB.BD.2.EXCISE.DUTY.AMT,AMT.FROM.POS>
                EXIT
            END
        END
        AMT.FROM.POS++
    REPEAT
    Y.SLAB.AMT=Y.SLAB.AMT
*
    RETURN
**************************************************************************
GEN.ENTRIES:
************
! Generate entries:
! Generate entries debiting the excise duty from loan contract and crediting
! it to the given internal account
! Form internal account

    CALL F.READ(FN.ACCOUNT,Y.INT.LIQ.ACCT,R.ACCT.REC,F.ACCOUNT,Y.ERR.AC)
    Y.INT.LIQ.CCY=R.ACCT.REC<AC.CURRENCY>
    Y.INTERNAL.ACCT=LCCY:Y.INTERNAL.ACCT.CAT:'0001'
    IF C$MULTI.BOOK THEN
        Y.INTERNAL.ACCT:= R.COMPANY(EB.COM.SUB.DIVISION.CODE)
    END
    IF NOT(R.ACCT.REC) OR Y.SLAB.AMT EQ '0' THEN
        RETURN
    END
    GOSUB CURR.CHK.4.DR.ENTRIES

! Statement entry debiting excise duty from the interest liquidation account
    ENTRY.REC=""
    ENTRY.REC<AC.STE.ACCOUNT.NUMBER>   = Y.INT.LIQ.ACCT
    ENTRY.REC<AC.STE.COMPANY.CODE>     = ID.COMPANY
    ENTRY.REC<AC.STE.AMOUNT.LCY>       = -1 * Y.LCY.DR.AMT
    ENTRY.REC<AC.STE.AMOUNT.FCY>       = Y.FCY.DR.AMT       ;* already marked in -sign
    ENTRY.REC<AC.STE.EXCHANGE.RATE>    = Y.EXC.RATE
    ENTRY.REC<AC.STE.TRANSACTION.CODE> = R.FT.COMMISSION.TYPE<FT4.TXN.CODE.DR>
    ENTRY.REC<AC.STE.THEIR.REFERENCE>  = Y.LD.ID
    ENTRY.REC<AC.STE.NARRATIVE,1>      ='Excise Duty'
    ENTRY.REC<AC.STE.CUSTOMER.ID>      = R.ACCT.REC<AC.CUSTOMER>
    ENTRY.REC<AC.STE.ACCOUNT.OFFICER>  = R.ACCT.REC<AC.ACCOUNT.OFFICER>
    ENTRY.REC<AC.STE.PRODUCT.CATEGORY> = R.ACCT.REC<AC.CATEGORY>
    ENTRY.REC<AC.STE.VALUE.DATE>       = TODAY
    ENTRY.REC<AC.STE.CURRENCY>         = R.ACCT.REC<AC.CURRENCY>
    ENTRY.REC<AC.STE.OUR.REFERENCE>    = Y.LD.ID
    ENTRY.REC<AC.STE.CURRENCY.MARKET>  = R.ACCT.REC<AC.CURRENCY.MARKET>
    ENTRY.REC<AC.STE.DEPARTMENT.CODE>  = "1"
    ENTRY.REC<AC.STE.EXPOSURE.DATE>    = TODAY
    ENTRY.REC<AC.STE.TRANS.REFERENCE>  = Y.LD.ID
    ENTRY.REC<AC.STE.SYSTEM.ID>        = Y.PARAM.ID
    ENTRY.REC<AC.STE.BOOKING.DATE>     = TODAY
    YENTRY.REC<-1> = LOWER(ENTRY.REC)

! Statement entry crediting excise duty to the internal account
    ENTRY.REC=""
    ENTRY.REC<AC.STE.ACCOUNT.NUMBER>   = Y.INTERNAL.ACCT
    ENTRY.REC<AC.STE.COMPANY.CODE>     = ID.COMPANY
    ENTRY.REC<AC.STE.AMOUNT.LCY>       = Y.LCY.DR.AMT
    ENTRY.REC<AC.STE.AMOUNT.FCY>       = ''
    ENTRY.REC<AC.STE.EXCHANGE.RATE>    = ''
    ENTRY.REC<AC.STE.TRANSACTION.CODE> = R.FT.COMMISSION.TYPE<FT4.TXN.CODE.CR>
    ENTRY.REC<AC.STE.THEIR.REFERENCE>  = Y.LD.ID
    ENTRY.REC<AC.STE.NARRATIVE,1>      = 'Excise Duty'
    ENTRY.REC<AC.STE.CUSTOMER.ID>      = R.ACCT.REC<AC.CUSTOMER>
    ENTRY.REC<AC.STE.ACCOUNT.OFFICER>  = R.ACCT.REC<AC.ACCOUNT.OFFICER>
    ENTRY.REC<AC.STE.PRODUCT.CATEGORY> = R.ACCT.REC<AC.CATEGORY>
    ENTRY.REC<AC.STE.VALUE.DATE>       = TODAY
    ENTRY.REC<AC.STE.CURRENCY>         = LCCY
    ENTRY.REC<AC.STE.OUR.REFERENCE>    = Y.LD.ID
    ENTRY.REC<AC.STE.CURRENCY.MARKET>  = R.ACCT.REC<AC.CURRENCY.MARKET>
    ENTRY.REC<AC.STE.DEPARTMENT.CODE>  = "1"
    ENTRY.REC<AC.STE.EXPOSURE.DATE>    = TODAY
    ENTRY.REC<AC.STE.TRANS.REFERENCE>  = Y.LD.ID
    ENTRY.REC<AC.STE.SYSTEM.ID>        = Y.PARAM.ID
    ENTRY.REC<AC.STE.BOOKING.DATE>     = TODAY
    YENTRY.REC<-1> = LOWER(ENTRY.REC)
*
    CALL EB.ACCOUNTING('LD','SAO',YENTRY.REC,'')
*
    RETURN
*-----------------------------------------------------------------------------
CURR.CHK.4.DR.ENTRIES:
*---------------------
* converting currency for db entries
    IF Y.INT.LIQ.CCY NE LCCY THEN
        Y.FCY.AMT=''
        Y.FCY.CUR=Y.INT.LIQ.CCY
        Y.RATE= ''
        Y.MARKET=''
        Y.LCY.DR.AMT=Y.SLAB.AMT
        YDIF.AMT=''
        Y.DIF.RATE=''
        CALL MIDDLE.RATE.CONV.CHECK(Y.FCY.AMT,Y.FCY.CUR,Y.RATE,Y.MARKET,Y.LCY.DR.AMT,YDIF.AMT,YDIF.RATE)
        Y.FCY.DR.AMT=Y.FCY.AMT
        Y.FCY.DR.AMT = -1 * Y.FCY.DR.AMT
        Y.EXC.RATE=Y.RATE
    END ELSE
        Y.LCY.DR.AMT =Y.SLAB.AMT
        Y.FCY.DR.AMT =''
    END
    RETURN
*--------------------------------------------------------------------
END
