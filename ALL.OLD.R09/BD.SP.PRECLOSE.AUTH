    SUBROUTINE BD.SP.PRECLOSE.AUTH
**AMEND DATE: 20190310
!PROGRAM  BD.SP.PRECLOSE.AUTH
    $INSERT GLOBUS.BP I_COMMON
    $INSERT GLOBUS.BP I_EQUATE
    $INSERT I_F.BD.H.SP.STOCK.ISSUE
    $INSERT SP.BP I_F.BD.H.SP.COUPON
    $INSERT GLOBUS.BP I_F.FUNDS.TRANSFER
    GOSUB INIT
    GOSUB OPENFILE
    GOSUB PROCESS
    RETURN

INIT:
    FN.SP.ISSUE='F.BD.H.SP.STOCK.ISSUE'
    F.SP.ISSUE=''
    FN.SP.COUPON = 'F.BD.H.SP.COUPON'
    F.SP.COUPON = ''
    Y.PRECLOSE.DATE=TODAY
    Y.NO.MONTH=''
    Y.SLAB=''
    Y.RATE=''
    Y.COUPON.AMT=0
    Y.FACE.VALUE=0
    Y.PAID.AMOUNT=0
    Y.SLAB.POS=''
    Y.COUP.NUM=0
    RETURN

OPENFILE:
    CALL OPF(FN.SP.ISSUE,F.SP.ISSUE)
    CALL OPF(FN.SP.COUPON,F.SP.COUPON)
    RETURN

PROCESS:
    Y.VERSION.ID=APPLICATION:PGM.VERSION
    IF Y.VERSION.ID EQ "FUNDS.TRANSFER,SP.PRECLOSE" THEN
        IF V$FUNCTION EQ 'A' THEN
            CALL GET.LOC.REF("FUNDS.TRANSFER","SP.INFO",Y.SP.POS)
            Y.SP.ID=R.NEW(FT.LOCAL.REF)<1,Y.SP.POS>
            Y.FT.ID=ID.NEW


            CALL F.READ(FN.SP.ISSUE,Y.SP.ID,R.SP.ISSUE,F.SP.ISSUE,Y.SP.ER)
            Y.FACE.VALUE=R.SP.ISSUE<SP.STK.FACE.VALUE>
            Y.ISSUE.DATE=R.SP.ISSUE<SP.STK.ISSUE.DATE>
            CALL EB.NO.OF.MONTHS(Y.ISSUE.DATE,Y.PRECLOSE.DATE,Y.NO.MONTH)

            IF Y.NO.MONTH GE 12 THEN
                GOSUB CALCULATE.SLAB
                GOSUB INTEREST.PAID
! Y.PAID.AMOUNT=Y.APPLICABLE.AMOUNT-Y.COUPON.AMT-Y.SOURCE.TAX
                IF Y.FACE.INT GT Y.COUP.AMT.TOT THEN
                    Y.PAY.AMT=Y.FACE.INT-Y.COUP.AMT.TOT
                    Y.PAY.SOURCE=Y.PAY.AMT*0.05
                    Y.PAY.AMT.TOT=Y.PAY.AMT-Y.PAY.SOURCE
                    Y.PAID.AMOUNT=Y.FACE.VALUE+Y.PAY.AMT.TOT
                END
                ELSE
                    Y.PAID.AMOUNT=Y.FACE.VALUE-Y.COUP.AMT.TOT
                END
            END
            ELSE
                GOSUB INTEREST.PAID
                Y.PAID.AMOUNT=Y.FACE.VALUE-Y.COUPON.AMT-Y.SOURCE.TAX
            END

            R.SP.ISSUE<SP.STK.APPL.AMT>=Y.PAID.AMOUNT
            R.SP.ISSUE<SP.STK.APPL.RATE>=Y.RATE
            R.SP.ISSUE<SP.STK.SP.INT.AMT>=Y.FACE.INT
! R.SP.ISSUE<SP.STK.SP.TAX>=Y.SLAB.SOURCE.TAX
            R.SP.ISSUE<SP.STK.SP.TAX>=Y.PAY.SOURCE
            R.SP.ISSUE<SP.STK.COUP.TAX>=Y.SOURCE.TAX
            R.SP.ISSUE<SP.STK.TRAN.REFRENCE2>=Y.FT.ID
            R.SP.ISSUE<SP.STK.MATURITY.STATUS>="PREMATURED"
            R.SP.ISSUE<SP.STK.STATUS>="RETURNED BY CUSTOMER"
            R.SP.ISSUE<SP.STK.CLOSE.DATE>=TODAY
            WRITE R.SP.ISSUE TO F.SP.ISSUE,Y.SP.ID
        END
    END
    RETURN
CALCULATE.SLAB:
    Y.MONTH=R.SP.ISSUE<SP.STK.NO.OF.MONTH>
    Y.FREQ.RATE=R.SP.ISSUE<SP.STK.FREQ.INTT.RATE>
    FOR I=1 TO DCOUNT(Y.MONTH,@VM)
        Y.MON=FIELD(Y.MONTH,@VM,I)
        Y.MON1=FIELD(Y.MONTH,@VM,I+1)
        Y.MON1=Y.MON1-1
        IF Y.NO.MONTH GE Y.MON AND Y.NO.MONTH LE Y.MON1 THEN
            Y.SLAB=Y.MON
            BREAK
        END
    NEXT I

    LOCATE Y.SLAB IN Y.MONTH<1,1> SETTING Y.SLAB.POS THEN
        Y.RATE=Y.FREQ.RATE<1,Y.SLAB.POS>
    END
    Y.YEAR=Y.SLAB/12
    Y.FACE.INT=(Y.FACE.VALUE*Y.RATE*Y.YEAR)/100
    Y.SLAB.SOURCE.TAX=Y.FACE.INT*0.05
!Y.FACE.AMT=Y.FACE.INT-Y.SLAB.SOURCE.TAX
!Y.APPLICABLE.AMOUNT=Y.FACE.VALUE+Y.FACE.AMT




    RETURN

INTEREST.PAID:

    Y.COUPON.ID=Y.SP.ID:"..."
    SEL.CMD = "SELECT ":FN.SP.COUPON:" WITH @ID LIKE ":Y.COUPON.ID
    CALL EB.READLIST(SEL.CMD,SEL.LIST,'',NO.OF.REC,RET.CODE)
    LOOP
        REMOVE Y.COUP.ID FROM SEL.LIST SETTING POS
    WHILE Y.COUP.ID:POS

        CALL F.READ(FN.SP.COUPON,Y.COUP.ID,R.COUPON,F.SP.COUPON,Y.COP.ER)
        Y.MAT.STATUS=R.COUPON<SP.CP.MATURITY.STATUS>
        Y.WITH.DATE=R.COUPON<SP.CP.WITHDRAW.DATE>

        IF Y.MAT.STATUS EQ 'MATURED' AND Y.WITH.DATE NE '' THEN
            Y.COUPON.AMT=Y.COUPON.AMT+R.COUPON<SP.CP.COUPON.AMT>
        END
        ELSE
            R.COUPON<SP.CP.MATURITY.STATUS>="PREMATURED"
            R.COUPON<SP.CP.STATUS>="RETURNED BY CUSTOMER"
            WRITE R.COUPON TO F.SP.COUPON,Y.COUP.ID
        END

    REPEAT
    Y.SOURCE.TAX=(Y.COUPON.AMT*5)/95
    Y.COUP.AMT.TOT=Y.COUPON.AMT+Y.SOURCE.TAX

    RETURN
