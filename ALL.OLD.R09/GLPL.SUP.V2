*-----------------------------------------------------------------------------
* <Rating>-31</Rating>
*-----------------------------------------------------------------------------
****************************************************************************************
*Developed By: Md. Zakir Hossain(JBL)*
*Date:20/02/2020*
****************************************************************************************

!PROGRAM GLPL.SUP.V2
    SUBROUTINE GLPL.SUP.V2(Y.RETURN)
    $INSERT GLOBUS.BP I_COMMON
    $INSERT GLOBUS.BP I_EQUATE
    $INSERT BP I_F.TODAY.ENTRY
    $INSERT GLOBUS.BP I_ENQUIRY.COMMON
    $INSERT GLOBUS.BP I_F.STMT.ENTRY
    $INSERT GLOBUS.BP I_F.ACCOUNT
    $INSERT GLOBUS.BP I_F.CATEG.ENTRY

    GOSUB INIT
    GOSUB OPENFILES
    GOSUB PROCESS

*------
INIT:
*------
    FN.TODAY.ENTRY="F.TODAY.ENTRY"
    F.TODAY.ENTRY=""
    FN.STMT.ENT = "F.STMT.ENTRY"
    F.STMT.ENT = ""
    FN.CATEG.ENT="F.CATEG.ENTRY"
    F.CATEG.ENT=""
    FN.AC = 'F.ACCOUNT'
    F.AC = ''
    FN.AC.HIS = 'F.ACCOUNT$HIS'
    F.AC.HIS = ''
    Y.CATEGORY=''
    Y.ALT.ACCT.ID=''
    Y.T24.ID=''
    Y.AC.TITLE=''
    Y.INST.NO=''
    Y.TRANS.AMOUNT=''
    Y.TRANS.REF=''
    FALSE = ""
!Y.DISALLOWCATEG=10001:@FM:14016:@FM:14032:@FM:14999:@FM:15201:@FM:12800
    RETURN

*---------
OPENFILES:
*---------
    CALL OPF(FN.TODAY.ENTRY,F.TODAY.ENTRY)
    CALL OPF(FN.STMT.ENT,F.STMT.ENT)
    CALL OPF(FN.AC,F.AC)
    CALL OPF(FN.AC.HIS,F.AC.HIS)

    RETURN

*-------
PROCESS:
*-------

    LOCATE "GL.PL" IN ENQ.SELECTION<2,1> SETTING GP.POS THEN
        Y.GL.PL = ENQ.SELECTION<4,GP.POS>
    END


    LOCATE "PRODUCT" IN ENQ.SELECTION<2,1> SETTING PROD.POS THEN
        Y.PRODUCT = ENQ.SELECTION<4,PROD.POS>
    END


    LOCATE "DEBIT.CREDIT" IN ENQ.SELECTION<2,1> SETTING DR.CR.POS THEN
        Y.DR.CR.MARK = ENQ.SELECTION<4,DR.CR.POS>
        IF Y.DR.CR.MARK EQ "DEBIT" THEN
            Y.DR.CR.MARK="DR"
        END
        IF Y.DR.CR.MARK EQ "CREDIT" THEN
            Y.DR.CR.MARK="CR"
        END
    END

    LOCATE "USER" IN ENQ.SELECTION<2,1> SETTING USER.POS THEN
        Y.USER = ENQ.SELECTION<4,USER.POS>
        CONVERT SM TO FM IN Y.USER
    END

    LOCATE "TRANSACTION.MODE" IN ENQ.SELECTION<2,1> SETTING MODE.POS THEN
        Y.MODE = ENQ.SELECTION<4,MODE.POS>
    END

    LOCATE "CURRENCY" IN ENQ.SELECTION<2,1> SETTING CURR.POS THEN
        Y.CURR = ENQ.SELECTION<4,CURR.POS>
    END
    IF Y.CURR EQ '' THEN
        Y.CURR='BDT'
    END

! Y.DR.CR.MARK='DR'
    IF Y.PRODUCT EQ '' OR Y.PRODUCT EQ 'ALL'  THEN
        SEL.CMD = "SELECT ":FN.TODAY.ENTRY:" WITH @ID LIKE ":RIGHT(ID.COMPANY,4):"*":RIGHT(ID.COMPANY,4):"*":Y.DR.CR.MARK:"*":Y.CURR:"*":Y.GL.PL:"*... AND @ID LIKE ...*":TODAY
    END ELSE
        SEL.CMD = "SELECT ":FN.TODAY.ENTRY:" WITH @ID LIKE ":RIGHT(ID.COMPANY,4):"*":RIGHT(ID.COMPANY,4):"*":Y.DR.CR.MARK:"*":Y.CURR:"*":Y.GL.PL:"*":Y.PRODUCT:"*... AND @ID LIKE ...*":TODAY
    END
    CALL EB.READLIST(SEL.CMD,SEL.LIST,"",NO.OF.RECORD,RET.CODE)
    LOOP
        REMOVE Y.REC.ID FROM SEL.LIST SETTING Y.POS
    WHILE Y.REC.ID:Y.POS
        Y.STMT.ID = FIELD(Y.REC.ID,"*",9)

        Y.ID.CATEG=FIELD(Y.REC.ID,"*",8)

! LOCATE Y.ID.CATEG IN Y.DISALLOWCATEG SETTING POS THEN
! CONTINUE
! END

        IF Y.GL.PL EQ "GL" THEN
            CALL F.READ(FN.STMT.ENT,Y.STMT.ID,R.STMT.REC,F.STMT.ENT,STMT.ERR)
            IF R.STMT.REC<AC.STE.RECORD.STATUS> EQ 'REVE' THEN
                CONTINUE
            END
!Y.CATEGORY=R.STMT.REC<AC.STE.PRODUCT.CATEGORY>
            Y.T24.ID=R.STMT.REC<AC.STE.ACCOUNT.NUMBER>
            IF Y.CURR EQ 'BDT' THEN
                Y.TRANS.AMOUNT=ABS(R.STMT.REC<AC.STE.AMOUNT.LCY>)
            END ELSE
                Y.TRANS.AMOUNT=ABS(R.STMT.REC<AC.STE.AMOUNT.FCY>)
            END
            Y.INPUT=FIELD(R.STMT.REC<AC.STE.INPUTTER>,"_",2)
            Y.AUTH=FIELD(R.STMT.REC<AC.STE.AUTHORISER>,"_",2)


        END ELSE
            CALL F.READ(FN.CATEG.ENT,Y.STMT.ID,R.CATEG.REC,F.CATEG.ENT,CATEG.ERR)
            IF R.CATEG.REC<AC.CAT.RECORD.STATUS> EQ 'REVE' THEN
                CONTINUE
            END

!Y.CATEGORY=R.CATEG.REC<AC.CAT.PL.CATEGORY>
            Y.T24.ID="PL":R.CATEG.REC<AC.CAT.PL.CATEGORY>
            IF Y.CURR EQ 'BDT' THEN
                Y.TRANS.AMOUNT=ABS(R.CATEG.REC<AC.CAT.AMOUNT.LCY>)
            END ELSE
                Y.TRANS.AMOUNT=ABS(R.CATEG.REC<AC.CAT.AMOUNT.FCY>)
            END
            Y.INPUT=FIELD(R.CATEG.REC<AC.CAT.INPUTTER>,"_",2)
            Y.AUTH=FIELD(R.CATEG.REC<AC.CAT.AUTHORISER>,"_",2)
        END
        IF Y.MODE NE "" THEN
            IF FIELD(Y.REC.ID,"*",7) NE Y.MODE THEN
                CONTINUE
            END
        END

        CALL F.READ(FN.TODAY.ENTRY,Y.REC.ID,R.STMT.TODAY,F.TODAY.ENTRY,TODAY.ERR)
        Y.INPUT.BY=R.STMT.TODAY<ENT.INPUT.BY>
        Y.CATEGORY =FIELD(Y.REC.ID,"*",8)

        IF Y.USER NE "" THEN
            LOCATE Y.INPUT.BY IN Y.USER SETTING POS1 THEN
            END ELSE
                CONTINUE
            END
!IF Y.USER NE Y.INPUT.BY THEN
!CONTINUE
! END
        END

        Y.ALT.ACCT.ID=R.STMT.TODAY<ENT.LEGACY.ID>
        Y.AC.TITLE=EREPLACE(R.STMT.TODAY<ENT.ACCT.TITLE>,"*","")
        Y.TRANS.REF=R.STMT.TODAY<ENT.TXN.ID>

        BEGIN CASE
        CASE FIELD(Y.REC.ID,"*",7) EQ "CH"
            Y.RETURN<-1> = Y.CATEGORY:"*":Y.ALT.ACCT.ID:"*":Y.T24.ID:"*":Y.AC.TITLE:"*":Y.TRANS.AMOUNT:"*":FALSE:"*":FALSE:"*":Y.TRANS.REF:"*":Y.INPUT:"*":Y.AUTH
        CASE FIELD(Y.REC.ID,"*",7) EQ "CL"
            Y.RETURN<-1> = Y.CATEGORY:"*":Y.ALT.ACCT.ID:"*":Y.T24.ID:"*":Y.AC.TITLE:"*":FALSE:"*":Y.TRANS.AMOUNT:"*":FALSE:"*":Y.TRANS.REF:"*":Y.INPUT:"*":Y.AUTH
        CASE FIELD(Y.REC.ID,"*",7) EQ "FT"
            Y.RETURN<-1> = Y.CATEGORY:"*":Y.ALT.ACCT.ID:"*":Y.T24.ID:"*":Y.AC.TITLE:"*":FALSE:"*":FALSE:"*":Y.TRANS.AMOUNT:"*":Y.TRANS.REF:"*":Y.INPUT:"*":Y.AUTH
        END CASE
!CRT Y.CATEGORY:"*":Y.ALT.ACCT.ID:"*":Y.T24.ID:"*":Y.AC.TITLE:"*":Y.INST.NO:"*":FALSE:"*":FALSE:"*":Y.TRANS.AMOUNT:"*":Y.TRANS.REF
    REPEAT
    Y.RETURN=SORT(Y.RETURN)
    RETURN
END

