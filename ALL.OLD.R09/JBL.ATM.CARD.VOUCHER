    SUBROUTINE JBL.ATM.CARD.VOUCHER
!PROGRAM JBL.ATM.CARD.VOUCHER
    $INSERT GLOBUS.BP I_COMMON
    $INSERT GLOBUS.BP I_EQUATE
    $INSERT GLOBUS.BP I_F.ACCOUNT
    $INSERT BP I_F.ATM.CARD.MGT
    $INSERT GLOBUS.BP I_F.FUNDS.TRANSFER
    $INSERT GLOBUS.BP I_F.FT.COMMISSION.TYPE
    $INSERT GLOBUS.BP I_F.ACCOUNT
    $INSERT GLOBUS.BP I_F.CATEGORY
    $INSERT GLOBUS.BP I_F.TAX
    $INSERT GLOBUS.BP I_ENQUIRY.COMMON

    FN.FT.NAU="F.FUNDS.TRANSFER$NAU"
    F.FT.NAU=""
    FN.FT="F.FUNDS.TRANSFER"
    F.FT=""
    FN.FT.COM.TYPE="F.FT.COMMISSION.TYPE"
    F.FT.COM.TYPE=""
    FN.AC = "F.ACCOUNT"
    F.AC = ""
    FN.TAX = "F.TAX"
    F.TAX = ""
    FN.CATEGORY = "F.CATEGORY"
    F.CATEGORY = ""

    CALL OPF(FN.FT.NAU,F.FT.NAU)
    CALL OPF(FN.FT,F.FT)
    CALL OPF(FN.AC,F.AC)
    CALL OPF(FN.TAX,F.TAX)
    CALL OPF(FN.CATEGORY,F.CATEGORY)
    CALL OPF(FN.FT.COM.TYPE,F.FT.COM.TYPE)

    TRN.REF=O.DATA

!DEBUG
    CALL F.READ(FN.FT.NAU,TRN.REF,REC.FT.REC,F.FT.NAU,ERR.FT)
    IF REC.FT.REC EQ "" THEN
        CALL F.READ(FN.FT,TRN.REF,REC.FT.REC,F.FT,ERR.FT.LIVE)
    END
    Y.COM.TYPE=REC.FT.REC<FT.COMMISSION.TYPE>
    Y.TAX.TYPE=REC.FT.REC<FT.TAX.TYPE>
    Y.DEBIT.AMT=REC.FT.REC<FT.AMOUNT.DEBITED>
    Y.CREDIT.ACCT=REC.FT.REC<FT.CREDIT.ACCT.NO>

    IF Y.CREDIT.ACCT NE "" THEN
        IF LEFT(Y.CREDIT.ACCT,3) EQ "BDT" THEN
            CALL F.READ(FN.AC,Y.CREDIT.ACCT,REC.ACC,F.AC,ERR.AC)
            Y.TITLE=REC.ACC<AC.ACCOUNT.TITLE.1>
            Y.CREDIT.TITLE=Y.TITLE
        END
        ELSE
            CALL F.READ(FN.CATEGORY,RIGHT(Y.CREDIT.ACCT,5),REC.CAT,F.CATEGORY,ERR.CAT)
            Y.TITLE=REC.CAT<EB.CAT.DESCRIPTION>
            Y.CREDIT.TITLE=Y.TITLE
        END

    END


    Y.DEBIT.AMT=OCONV(Y.DEBIT.AMT,"MC/A" )
    Y.AMT=Y.DEBIT.AMT
    GOSUB DO.TRANSACTION
    Y.DEBIT.AMT=Y.AMT

    IF Y.COM.TYPE NE "" THEN
        CALL F.READ(FN.FT.COM.TYPE,Y.COM.TYPE,REC.FT.COM.TYPE,F.FT.COM.TYPE,ERR.FT.TYPE)
        Y.CATEGORY.ACCOUNT=REC.FT.COM.TYPE<FT4.CATEGORY.ACCOUNT>
        IF LEFT(Y.CATEGORY.ACCOUNT,3) EQ "BDT" THEN
            Y.ACCT=LEFT(Y.CATEGORY.ACCOUNT,12): RIGHT(ID.COMPANY,4)
            CALL F.READ(FN.AC,Y.ACCT,REC.ACC,F.AC,ERR.AC)
            Y.TITLE=REC.ACC<AC.ACCOUNT.TITLE.1>
            Y.AC.TITLE=Y.TITLE:"( ":Y.ACCT:" )"
        END
        ELSE
            CALL F.READ(FN.CATEGORY,Y.CATEGORY.ACCOUNT,REC.CAT,F.CATEGORY,ERR.CAT)
            Y.TITLE=REC.CAT<EB.CAT.DESCRIPTION>
            Y.AC.TITLE=Y.TITLE:"( ":"PL ":LEFT(Y.CATEGORY.ACCOUNT,2):"-":RIGHT(Y.CATEGORY.ACCOUNT,3): " )"
        END
        Y.COM.AMT=REC.FT.REC<FT.COMMISSION.AMT>
        Y.COM.AMT=OCONV(Y.COM.AMT,"MC/A" )
        Y.AMT=Y.COM.AMT
        GOSUB DO.TRANSACTION
        Y.COM.AMT=Y.AMT
    END
    IF Y.TAX.TYPE NE "" THEN
        SEL.TAX="SELECT ":FN.TAX:" WITH @ID LIKE ":Y.TAX.TYPE:".... BY-DSND @ID"
        CALL EB.READLIST(SEL.TAX,TAX.LIST,'',NO.OF.TAX,TAX.ERR)
        Y.TAX.ID=TAX.LIST<1>
        CALL F.READ(FN.TAX,Y.TAX.ID,REC.TAX,F.TAX,ERR.TAX)
        Y.TAX.CAT=REC.TAX<EB.TAX.CATEGORY>
        Y.TAX.ACCT="BDT":Y.TAX.CAT:"0001":RIGHT(ID.COMPANY,4)
        CALL F.READ(FN.AC,Y.TAX.ACCT,REC.ACC,F.AC,ERR.AC)
        Y.TAX.TITLE=REC.ACC<AC.ACCOUNT.TITLE.1>
        Y.TAX.AC.TITLE=Y.TAX.TITLE:"( ":Y.TAX.ACCT:" )"
        Y.TAX.AMT=REC.FT.REC<FT.TAX.AMT>
        Y.TAX.AMT=OCONV(Y.TAX.AMT,"MC/A" )
        Y.AMT=Y.TAX.AMT
        GOSUB DO.TRANSACTION
        Y.TAX.AMT=Y.AMT
    END
    O.DATA=Y.AC.TITLE:"|":Y.TAX.AC.TITLE:"|":Y.DEBIT.AMT : "|" :Y.COM.AMT :"|":Y.TAX.AMT:"|":Y.CREDIT.TITLE

    RETURN

DO.TRANSACTION:

    LNGVAR = Y.AMT
    TXTOUT = ''
    TXTVAR1=''
    INTVAL=''


    INTVAL = FIELD(LNGVAR,'.',1)
    INTVAL3 = FIELD(LNGVAR,'.',2)

    IF INTVAL3 NE 0 THEN
        INTVAL2=INTVAL3
    END ELSE
        INTVAL2=0
    END

    CORE=INT(INTVAL / 10000000)
    CALL CALHUND(CORE,INTCORE)
    INTVAL = INT(INTVAL - INT(INTVAL / 10000000) * 10000000)

    LAC=INT(INTVAL / 100000)
    CALL CALHUND(LAC,INTLAC)
    INTVAL = INT(INTVAL - INT(INTVAL / 100000) * 100000)

    THOUSAND=INT(INTVAL / 1000)
    CALL CALHUND(THOUSAND,INTTHOUSAND)
    INTVAL = INT(INTVAL - INT(INTVAL / 1000) * 1000)

    HUNDRED=INT(INTVAL / 100)
    CALL CALHUND(HUNDRED,INTHUNDRED)
    INTVAL = INT(INTVAL - INT(INTVAL / 100) * 100)

    REST=INT(INTVAL / 1)
    CALL CALHUND(REST,INTREST)

    DES=INT(INTVAL2 / 1)
    CALL CALHUND(DES,INTDES)

    IF LEN(INTCORE) EQ 0 THEN
        TXTVAR1=INTCORE:" ":""
    END ELSE
        TXTVAR1=INTCORE:" ":"Core"
    END

    IF LEN(INTLAC) EQ 0 THEN
        TXTVAR1=TXTVAR1:" ":INTLAC:"":""
    END ELSE
        TXTVAR1=TXTVAR1:" ":INTLAC:" ":"Lac"
    END

    IF LEN(INTTHOUSAND) EQ 0 THEN
        TXTVAR1=TXTVAR1:" ":INTTHOUSAND:"":""
    END ELSE
        TXTVAR1=TXTVAR1:" ":INTTHOUSAND:" ":"Thousand"
    END

    IF LEN(INTHUNDRED) EQ 0 THEN
        TXTVAR1=TXTVAR1:" ":INTHUNDRED:"":""
    END ELSE
        TXTVAR1=TXTVAR1:" ":INTHUNDRED:" ":"Hundred"
    END

    TXTVAR1=TXTVAR1:" ":INTREST:" ":"Taka"

    IF LEN(INTDES) EQ 0 THEN
        TXTVAR1=TXTVAR1:""
    END ELSE
        TXTVAR1=TXTVAR1:" ":"and":" ":INTDES:" ":"Paisa"
    END

    Y.AMT = TXTVAR1

    RETURN

END
