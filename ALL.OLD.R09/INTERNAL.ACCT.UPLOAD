    SUBROUTINE INTERNAL.ACCT.UPLOAD

    $INSERT GLOBUS.BP I_EQUATE
    $INSERT GLOBUS.BP I_COMMON
    $INSERT GLOBUS.BP I_F.ACCOUNT

    GOSUB INIT
    GOSUB WRITE.ERROR.LOG
    RETURN

INIT:
    FN.AC = 'F.ACCOUNT'
    F.AC = ''
    CALL OPF(FN.AC,F.AC)

    CRT "PLEASE INPUT DIRECTORY NAME"
    INPUT Y.UPLOAD.DIR
    CRT "PLEASE INPUT FILE NAME"
    INPUT Y.UPLOAD.FILE

    Y.ERROR.FILE = 'ERROR.LOG.txt'
    GOSUB FILE.INIT

    Y.FILE = Y.UPLOAD.DIR:'/':Y.UPLOAD.FILE
    Y.FILE.VAR = ''
    GOSUB READ.SEQFILE

    RETURN

FILE.INIT:
    OPEN Y.UPLOAD.DIR TO F.Y.UPLOAD.DIR ELSE
        CMD = "CREATE.FILE ":Y.UPLOAD.DIR:" TYPE=UD"
        EXECUTE CMD
        OPEN Y.UPLOAD.DIR TO F.Y.UPLOAD.DIR ELSE
            ETEXT = "OPENING OF ":Y.UPLOAD.DIR:" FAILED"
            CALL STORE.END.ERROR
            RETURN
        END
    END
    RETURN

READ.SEQFILE:
    OPENSEQ Y.FILE TO Y.FILE.VAR THEN
        LOOP
            READSEQ L.VALUES FROM Y.FILE.VAR ELSE
                L.VALUES = ''
            END
        WHILE L.VALUES
            GOSUB RECORD.BUILD
            GOSUB OFS.STRING
            GOSUB OFS.VALIDATE
        REPEAT
        CLOSESEQ Y.FILE.VAR
    END
    ELSE
        ETEXT = "File ":Y.UPLOAD.FILE:" not found at ":Y.UPLOAD.DIR:" path"
        CALL STORE.END.ERROR
        RETURN
    END

    RETURN

RECORD.BUILD:
    Y.ACCT.ID = FIELD(L.VALUES,',',1)
    Y.CUSTOMER = FIELD(L.VALUES,',',2)
    Y.ACCOUNT.TITLE.1 = FIELD(L.VALUES,',',3)
    Y.ACCOUNT.TITLE.2 = FIELD(L.VALUES,',',4)
    Y.SHORT.TITLE = FIELD(L.VALUES,',',5)
    Y.CATEGORY = FIELD(L.VALUES,',',6)
    Y.CURRENCY = FIELD(L.VALUES,',',7)
    Y.JOINT.HOLDER = FIELD(L.VALUES,',',8)
    Y.RELATION.CODE = FIELD(L.VALUES,',',9)
    Y.PASSBOOK = FIELD(L.VALUES,',',10)
    Y.OPENING.DATE = FIELD(L.VALUES,',',11)
    Y.MNEMONIC = FIELD(L.VALUES,',',12)
    Y.RECONCILE.ACCT = FIELD(L.VALUES,',',13)
    Y.NOMINEE.NAME = FIELD(L.VALUES,',',14)
    Y.NOM.RELATION = FIELD(L.VALUES,',',15)
    Y.NOM.PERCENT = FIELD(L.VALUES,',',16)
    Y.INTRODUC.ACC.NO = FIELD(L.VALUES,',',17)
    Y.INTRODUCER.NAME = FIELD(L.VALUES,',',18)
    Y.AUTH.SIGNATORY = FIELD(L.VALUES,',',19)
    Y.ALT.ACCT.ID = FIELD(L.VALUES,',',20)
    Y.UPLOAD.COMPANY = FIELD(L.VALUES,',',21)
    Y.ACCOUNT.OFFICER = '1'

    RETURN

OFS.STRING:
    Y.OFS.STRING = "ACCOUNT.TITLE.1::=":Y.ACCOUNT.TITLE.1:","
    Y.OFS.STRING := "ACCOUNT.TITLE.2::=":Y.ACCOUNT.TITLE.2:","
    Y.OFS.STRING := "SHORT.TITLE::=":Y.SHORT.TITLE:","
    Y.OFS.STRING := "CATEGORY::=":Y.CATEGORY:","
    Y.OFS.STRING := "CURRENCY::=":Y.CURRENCY:","
    Y.OFS.STRING := "JOINT.HOLDER::=":Y.JOINT.HOLDER:","
    Y.OFS.STRING := "RELATION.CODE::=":Y.RELATION.CODE:","
    Y.OFS.STRING := "PASSBOOK::=":Y.PASSBOOK:","
    Y.OFS.STRING := "OPENING.DATE::=":Y.OPENING.DATE:","
    Y.OFS.STRING := "MNEMONIC::=":Y.MNEMONIC:","
    Y.OFS.STRING := "RECONCILE.ACCT::=":Y.RECONCILE.ACCT:","
    Y.OFS.STRING := "NOMINEE.NAME::=":Y.NOMINEE.NAME:","
    Y.OFS.STRING := "NOM.RELATION::=":Y.NOM.RELATION:","
    Y.OFS.STRING := "NOM.PERCENT::=":Y.NOM.PERCENT:","
    Y.OFS.STRING := "INTRODUC.ACC.NO::=":Y.INTRODUC.ACC.NO:","
    Y.OFS.STRING := "INTRODUCER.NAME::=":Y.INTRODUCER.NAME:","
    Y.OFS.STRING := "AUTH.SIGNATORY::=":Y.AUTH.SIGNATORY:","
    Y.OFS.STRING := "ALT.ACCT.ID::=":Y.ALT.ACCT.ID:","
    Y.OFS.STRING := "ACCOUNT.OFFICER::=":Y.ACCOUNT.OFFICER:","
    Y.OFS.STRING := "CO.CODE::=":Y.UPLOAD.COMPANY

    RETURN

OFS.VALIDATE:
    Y.USER = 'DMUSER.1'
    Y.OFS.SRC = 'DM.OFS.SRC'
    Y.BR.CODE = ID.COMPANY
    IF Y.BR.CODE NE Y.UPLOAD.COMPANY THEN
        Y.BR.CODE = Y.UPLOAD.COMPANY
        CALL LOAD.COMPANY(Y.BR.CODE)
    END

    Y.OFS.MSG.VAL = "ACCOUNT,MIG/I/PROCESS,":Y.USER:"//,":Y.ACCT.ID:",":Y.OFS.STRING
    CALL OFS.GLOBUS.MANAGER(Y.OFS.SRC,Y.OFS.MSG.VAL)
    Y.OFS.MSG.STATUS = FIELD(Y.OFS.MSG.VAL,'/',3)

    IF Y.OFS.MSG.STATUS EQ -1 THEN
        Y.ERR.LOG<-1> = Y.OFS.MSG.VAL:'*':L.VALUES
    END
    ELSE
        PRINT "ACCOUNT ID>>":Y.ACCT.ID
    END

    RETURN

WRITE.ERROR.LOG:
    WRITE Y.ERR.LOG TO F.Y.UPLOAD.DIR,Y.ERROR.FILE
    CALL JOURNAL.UPDATE('')

    RETURN

END
