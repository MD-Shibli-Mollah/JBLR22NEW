*-----------------------------------------------------------------------------
* <Rating>431</Rating>
*-----------------------------------------------------------------------------
    SUBROUTINE PR.VAU.UPDATE.INSTR.COLLECTION
    $INSERT I_COMMON
    $INSERT I_EQUATE
    $INSERT I_F.TELLER
    $INSERT I_F.FUNDS.TRANSFER
    $INSERT I_F.MNEMONIC.COMPANY
    $INSERT I_F.CHEQUE.REGISTER
    $INSERT I_F.CHEQUES.PRESENTED
    $INSERT JBL.BP I_F.PR.H.INSTR.ISSUED
    $INSERT I_F.CHEQUE.TYPE.ACCOUNT
    $INSERT JBL.BP I_F.PR.H.INSTRUMENT

    GOSUB PROCESS

    RETURN

OPEN.FILES:
*==========

*    FN.PR.H.INSTR.ISSUED = 'F':Y.BR.MNE:'.PR.H.INSTR.ISSUED'

    FN.PR.H.INSTR.ISSUED = 'F.PR.H.INSTR.ISSUED'
    F.PR.H.INSTR.ISSUED = ''
    CALL OPF(FN.PR.H.INSTR.ISSUED,F.PR.H.INSTR.ISSUED)

*    FN.PR.H.CHEQUES.PRESENTED = 'F':Y.BR.MNE:'.CHEQUES.PRESENTED'
    FN.PR.H.CHEQUES.PRESENTED = 'F.CHEQUES.PRESENTED'
    F.PR.H.CHEQUES.PRESENTED = ''

    CALL OPF(FN.PR.H.CHEQUES.PRESENTED,F.PR.H.CHEQUES.PRESENTED)

    RETURN

PROCESS:
*=======

    IF APPLICATION EQ 'FUNDS.TRANSFER' THEN
        Y.DEBIT.ACCT = R.NEW(FT.DEBIT.ACCT.NO)
        Y.CHEQUE.TYPE = R.NEW(FT.CHEQ.TYPE)
        Y.CHEQUE.NO = R.NEW(FT.CHEQUE.NUMBER)
        DEBIT.VAL.DT = R.NEW(FT.DEBIT.VALUE.DATE)
        Y.REC.STATUS = R.NEW(FT.RECORD.STATUS)
    END ELSE
        Y.DEBIT.ACCT = R.NEW(TT.TE.ACCOUNT.1)
        Y.CHEQUE.TYPE = R.NEW(TT.TE.CHEQ.TYPE)
        Y.CHEQUE.NO = R.NEW(TT.TE.CHEQUE.NUMBER)
        DEBIT.VAL.DT = R.NEW(TT.TE.VALUE.DATE.2)
        Y.REC.STATUS = R.NEW(TT.TE.RECORD.STATUS)
    END
    IF PGM.VERSION EQ ',PR.DD.COLLECTION' THEN
        Y.BRANCH = 'BRANCH'
        Y.BRANCH.POS = ''
        CALL GET.LOC.REF(APPLICATION,Y.BRANCH,Y.BRANCH.POS)
        Y.BR.MNE = R.NEW(LOCAL.REF.FIELD)<1,Y.BRANCH.POS>

*        Y.CHQ.NO = 'PR.CHEQUE.NO'
*        Y.CHQ.POS = ''
*        CALL GET.LOC.REF(APPLICATION,Y.CHQ.NO,Y.CHQ.POS)
*        Y.CHEQUE.NO = R.NEW(LOCAL.REF.FIELD)<1,Y.CHQ.POS>


        Y.BR.CODE = ''
        CALL DBR("MNEMONIC.COMPANY":FM:AC.MCO.COMPANY,Y.BR.MNE,Y.BR.CODE)
        FN.PR.DD.INSTRUMENT = 'F':Y.BR.MNE:'.PR.H.INSTRUMENT'
        F.PR.DD.INSTRUMENT = ''
        CALL OPF(FN.PR.DD.INSTRUMENT,F.PR.DD.INSTRUMENT)
        Y.PR.INSTR = Y.BR.CODE
        R.PR.INSTR = ''
        Y.INSTR.READ.ERR = ''
        CALL F.READ(FN.PR.DD.INSTRUMENT,Y.PR.INSTR,R.PR.INSTR,F.PR.DD.INSTRUMENT,Y.INSTR.READ.ERR)
        Y.DEBIT.ACCT = R.PR.INSTR<INS.DD.ACCOUNT>

        IF NOT(Y.DEBIT.ACCT) THEN
            E = "Instrument Parameter not setup for Company ":Y.BR.MNE
            RETURN
        END
        Y.CHEQUE.TYPE = ''
        CALL DBR("CHEQUE.TYPE.ACCOUNT":FM:CHQ.TYP.CHEQUE.TYPE,Y.DEBIT.ACCT,Y.CHEQUE.TYPE)
        IF NOT(Y.CHEQUE.TYPE) THEN
            E = "Cheque Type missing for Account No.":Y.DEBIT.ACCT
            RETURN
        END

*        FN.CHEQUE.REGISTER = 'F':Y.BR.MNE:'.CHEQUE.REGISTER'

        FN.CHEQUE.REGISTER = 'F.CHEQUE.REGISTER'
        F.CHEQUE.REGISTER = ''
        CALL OPF(FN.CHEQUE.REGISTER,F.CHEQUE.REGISTER)

*        FN.CHEQUES.STOPPED = 'F':Y.BR.MNE:'.CHEQUES.STOPPED'

        FN.CHEQUES.STOPPED = 'F.CHEQUES.STOPPED'
        F.CHEQUES.STOPPED = ''
        CALL OPF(FN.CHEQUES.STOPPED,F.CHEQUES.STOPPED)

*        FN.CHEQUES.PRESENTED = 'F':Y.BR.MNE:'.CHEQUES.PRESENTED'

        FN.CHEQUES.PRESENTED = 'F.CHEQUES.PRESENTED'
        F.CHEQUES.PRESENTED = ''
        CALL OPF(FN.CHEQUES.PRESENTED,F.CHEQUES.PRESENTED)

        Y.CHQ.REG.ID = Y.CHEQUE.TYPE:'.':Y.DEBIT.ACCT
        CALL F.READ(FN.CHEQUE.REGISTER,Y.CHQ.REG.ID,R.CHEQUE.REGISTER,F.CHEQUE.REGISTER,CR.READ.ERR)
        IF NOT(CR.READ.ERR) THEN
            CR.ISSUE.RANGE = R.CHEQUE.REGISTER<CHEQUE.REG.CHEQUE.NOS>
            CR.ISSUE.RANGE.CNT = DCOUNT(CR.ISSUE.RANGE,@VM)
            Y.START.NO = ''
            FOR I = 1 TO CR.ISSUE.RANGE.CNT
                Y.RANGE.FLD = ''
                Y.RANGE.FLD = CR.ISSUE.RANGE<1,I>
                Y.START.NO = Y.CHEQUE.NO
                Y.END.NO = ''
                Y.RESULT = ''
                Y.ERROR = ''
                CALL EB.MAINTAIN.RANGES(Y.RANGE.FLD,Y.START.NO,Y.END.NO,'ENQ',Y.RESULT,Y.ERROR)
                IF Y.RESULT EQ 1 THEN EXIT
            NEXT I
            IF NOT(Y.RESULT) THEN
                E = "CHEQUE NUMBER ":Y.CHEQUE.NO:" NOT ISSUED TO THE ACCOUNT ":Y.DEBIT.ACCT
                RETURN
            END
            CR.RET.RANGE = R.CHEQUE.REGISTER<CHEQUE.REG.RETURNED.CHQS>
            LOCATE Y.CHEQUE.NO IN CR.RET.RANGE<1,1> SETTING Y.RET.RG.POS THEN
                E = "CHEQUE NUMBER ":Y.CHEQUE.NO:" ALREADY CANCELLED"
                RETURN
            END

            Y.CHQ.PRESENTED.ID = Y.CHEQUE.TYPE:'.':Y.DEBIT.ACCT:'-':Y.CHEQUE.NO
            CALL F.READ(FN.CHEQUES.PRESENTED,Y.CHQ.PRESENTED.ID,R.CHQ.PRESENT,F.CHEQUES.PRESENTED,CHQ.PRESENT.READ.ERR)
            IF R.CHQ.PRESENT THEN
                E = "CHEQUE NUMBER ":Y.CHEQUE.NO:" ALREADY PRESENTED ON ":R.CHQ.PRESENT<CHQ.PRE.DATE.PRESENTED,1>
                RETURN
            END

            Y.CHQ.STOPPED.ID = Y.DEBIT.ACCT:'*':Y.CHEQUE.NO
            CALL F.READ(FN.CHEQUES.STOPPED,Y.CHQ.STOPPED.ID,R.CHQ.STOPPED,F.CHEQUES.STOPPED,CHQ.STOP.READ.ERR)
            IF R.CHQ.STOPPED THEN
                E = "CHEQUE NUMBER ":Y.CHEQUE.NO:" ALREADY STOPPED"
                RETURN
            END
        END ELSE
            E = "CHEQUE REGISTER NOT AVAIALBLE FOR ACCOUNT NUMBER ":Y.DEBIT.ACCT
            RETURN
        END
    END ELSE
        Y.BR.MNE = ''
        Y.BR.CO.CODE = ''
        CALL GET.ACCT.BRANCH(Y.DEBIT.ACCT,Y.BR.MNE,Y.BR.CO.CODE)
        IF NOT(Y.BR.MNE) THEN
            E = "Instrument Parameter not setup for Company ":Y.BR.MNE
            RETURN
        END
    END

    GOSUB OPEN.FILES
    GOSUB UPDATE.INSTR.ISSUED

    RETURN

UPDATE.INSTR.ISSUED:
*===================
    Y.INS.ID = Y.CHEQUE.TYPE:'.':Y.DEBIT.ACCT:'-':Y.CHEQUE.NO
    R.INSTR.ISSUED = ''
    IF Y.REC.STATUS[1,2] EQ 'IN' THEN
        CALL F.READ(FN.PR.H.INSTR.ISSUED,Y.INS.ID,R.INSTR.ISSUED,F.PR.H.INSTR.ISSUED,INSTR.READ.ERR)
        IF R.INSTR.ISSUED THEN
            R.INSTR.ISSUED<INS.DATE.OF.PRESENTED> = DEBIT.VAL.DT
            R.INSTR.ISSUED<INS.COLL.TXN.REF> = ID.NEW
            R.INSTR.ISSUED<INS.COLL.TXN.COMPANY> = ID.COMPANY
            GOSUB UPD.CHQ.PRESENTED
        END ELSE
            E = "CHEQUE NUMBER ":Y.CHEQUE.NO:" NOT ISSUED FOR ":Y.DEBIT.ACCT
            RETURN
        END
    END ELSE
        CALL F.READ(FN.PR.H.INSTR.ISSUED,Y.INS.ID,R.INSTR.ISSUED,F.PR.H.INSTR.ISSUED,INSTR.READ.ERR)
        IF R.INSTR.ISSUED THEN
            R.INSTR.ISSUED<INS.DATE.OF.PRESENTED> = ''
            R.INSTR.ISSUED<INS.COLL.TXN.REF> = ''
            R.INSTR.ISSUED<INS.COLL.TXN.COMPANY> = ''
            GOSUB UPD.CHQ.PRESENTED
        END ELSE
            E = "CHEQUE NUMBER ":Y.CHEQUE.NO:" NOT ISSUED FOR ":Y.DEBIT.ACCT
            RETURN
        END
    END
    CALL F.WRITE(FN.PR.H.INSTR.ISSUED,Y.INS.ID,R.INSTR.ISSUED)

    RETURN

UPD.CHQ.PRESENTED:
*=================
    IF PGM.VERSION EQ ',PR.DD.COLLECTION' THEN
        Y.CHEQUES.PRESENTED = Y.INS.ID
        IF Y.REC.STATUS[1,2] EQ 'IN' THEN
            R.CHEQUES.PRESENTED = ''
            CALL F.READ(FN.PR.H.CHEQUES.PRESENTED,Y.CHEQUES.PRESENTED,R.CHEQUES.PRESENTED,F.PR.H.CHEQUES.PRESENTED,Y.CHQ.READ.ERR)
            IF R.CHEQUES.PRESENTED THEN
                R.CHEQUES.PRESENTED<CHQ.PRE.DATE.PRESENTED> = DEBIT.VAL.DT
                R.CHEQUES.PRESENTED<CHQ.PRE.REPRESENTED.COUNT> = 1
            END ELSE
                Y.CHQ.PRE.CNT = 0
                Y.CHQ.PRE.CNT = DCOUNT(R.CHEQUES.PRESENTED<CHQ.PRE.DATE.PRESENTED>,@VM) + 1
                R.CHEQUES.PRESENTED<CHQ.PRE.DATE.PRESENTED,Y.CHQ.PRE.CNT> = DEBIT.VAL.DT
            END
            CALL F.WRITE(FN.PR.H.CHEQUES.PRESENTED,Y.CHEQUES.PRESENTED,R.PR.H.CHEQUES.PRESENTED)

        END ELSE
            DELETE F.PR.H.CHEQUES.PRESENTED,Y.CHEQUES.PRESENTED
        END
    END

    RETURN
END
