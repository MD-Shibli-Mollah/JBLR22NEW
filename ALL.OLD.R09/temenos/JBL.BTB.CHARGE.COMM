    SUBROUTINE JBL.BTB.CHARGE.COMM(Y.RETURN)
        !PROGRAM JBL.BTB.CHARGE.COMM
    $INSERT GLOBUS.BP I_COMMON
    $INSERT GLOBUS.BP I_EQUATE
    $INSERT GLOBUS.BP I_ENQUIRY.COMMON
    $INSERT GLOBUS.BP I_GTS.COMMON
    $INSERT GLOBUS.BP I_BATCH.FILES
    $INSERT GLOBUS.BP I_F.LETTER.OF.CREDIT
    $INSERT GLOBUS.BP I_F.LC.ACCOUNT.BALANCES
    $INSERT JBL.BP I_F.BD.BTB.JOB.REGISTER

    GOSUB INIT
    GOSUB OPENFILES
    GOSUB PROCESS
    RETURN

*----
INIT:
*----

    FN.ACCT = 'FBNK.LETTER.OF.CREDIT'
    F.ACCT = ''
    FN.LC.ACCT.BAL = 'FBNK.LC.ACCOUNT.BALANCES'
    F.LC.ACCT.BAL = ''
    FN.REGISTER='F.BD.BTB.JOB.REGISTER'
    F.REGISTER=''
    LOCATE "UCP.IND" IN ENQ.SELECTION<2,1> SETTING IND.POS THEN
        Y.IND=ENQ.SELECTION<4,IND.POS>
    END
    LOCATE "OLD.LC.NUMBER" IN ENQ.SELECTION<2,1> SETTING OLD.LC.POS THEN
        Y.BTB.LC.NUMBER=ENQ.SELECTION<4,OLD.LC.POS>
    END
    LOCATE "TENOR" IN ENQ.SELECTION<2,1> SETTING TENOR.POS THEN
        Y.EXP.LC.NUMBER=ENQ.SELECTION<4,TENOR.POS>
    END
    LOCATE "POSITION.TYPE" IN ENQ.SELECTION<2,1> SETTING PTYPE.POS THEN
        Y.EXP.TF=ENQ.SELECTION<4,PTYPE.POS>
    END
    LOCATE "APPLICANT.CUSTNO" IN ENQ.SELECTION<2,1> SETTING CUS.POS THEN
        Y.CUS=ENQ.SELECTION<4,CUS.POS>
    END
    RETURN
*---------
OPENFILES:
*---------
    CALL OPF(FN.ACCT,F.ACCT)
    CALL OPF(FN.LC.ACCT.BAL,F.LC.ACCT.BAL)
    CALL OPF(FN.REGISTER,F.REGISTER)
    CALL GET.LOC.REF("LETTER.OF.CREDIT","JOB.NUMBER",Y.JOB.NUMBER.POS)
!-------------------------------
    RETURN
*-------
PROCESS:
*-------
! SEL.CMD='SELECT ':FN.ACCT:' WITH LC.TYPE EQ BFUC BFUU FLUC BLUU ZFUC ZFUU ZLUC ZLUU'
    SEL.CMD='SELECT ':FN.ACCT:' WITH LC.TYPE EQ BFSC BFSU BLSC BLSU GFSC GFSU GLSC GLSU BFUC BFUU BLUC BLUU ZFUC ZFUU ZLUC ZLUU EFSC EFSU ELSC ELSU ZFSC ZFSU ZLSC ZLSU AND CO.CODE EQ ':ID.COMPANY
!   DEBUG
!SEL.CMD='SELECT ':FN.ACCT:' WITH LC.TYPE EQ BFUC BFUU FLUC BLUU ZFUC ZFUU ZLUC ZLUU AND CO.CODE EQ ':BD0010102

    IF Y.IND NE '' THEN
        SEL.CMD :=" AND @ID EQ ":Y.IND
    END
    LOCATE 'ISSUE.DATE' IN ENQ.SELECTION<2,1> SETTING DT.POS THEN
        IF ENQ.SELECTION<4,DT.POS> THEN
            SEL.CMD :=" AND ISSUE.DATE GE ":ENQ.SELECTION<4,DT.POS,1>:" AND ISSUE.DATE LE ":ENQ.SELECTION<4,DT.POS,2>
        END
    END

    CALL EB.READLIST(SEL.CMD,SEL.LIST,'',NO.OF.REC,RET.CODE)

    k=1
    LOOP
        REMOVE Y.REC.ID FROM SEL.LIST SETTING Y.POS
    WHILE Y.REC.ID:Y.POS
        CALL F.READ(FN.ACCT,Y.REC.ID,R.ACCT,F.ACCT,ACCT.ERR)
        CALL F.READ(FN.LC.ACCT.BAL,Y.REC.ID,R.LC.ACCT.BAL,F.LC.ACCT.BAL,LC.ACCT.BAL.ERR)
        Y.JOB.NUMBER = R.ACCT<TF.LC.LOCAL.REF,Y.JOB.NUMBER.POS>
        CALL F.READ(FN.REGISTER,Y.JOB.NUMBER,R.REGISTER,F.REGISTER,REGISTER.ERR)

        IF Y.BTB.LC.NUMBER NE '' THEN
            IF Y.BTB.LC.NUMBER NE R.ACCT<TF.LC.OLD.LC.NUMBER> THEN
                CONTINUE
            END
        END

        IF Y.EXP.LC.NUMBER NE '' THEN
            IF Y.EXP.LC.NUMBER NE R.REGISTER<BTB.JOB.EX.LC.NUMBER> THEN
                CONTINUE
            END
        END
        IF Y.EXP.TF NE '' THEN
            IF Y.EXP.TF NE R.REGISTER<BTB.JOB.EX.TF.REF> THEN
                CONTINUE
            END
        END
        IF Y.CUS NE '' THEN
            IF Y.CUS NE R.ACCT<TF.LC.APPLICANT.CUSTNO> THEN
                CONTINUE
            END
        END
!      CRT 'tEST ##':R.REGISTER<BTB.JOB.EX.TF.REF>:' ###':R.REGISTER<BTB.JOB.EX.LC.NUMBER>
        Y.COMM.AMT=''
        Y.COMM.AMT.ST=''
        Y.SWIFT.CHRG=''
        FOR I = 1 TO DCOUNT(R.LC.ACCT.BAL<LCAC.CHRG.CODE>,@VM)
            Y.COMM.CODE = FIELD(R.LC.ACCT.BAL<LCAC.CHRG.CODE>,@VM,I)
            IF (Y.COMM.CODE EQ 'LCOBTBV') THEN
                Y.COMM.AMT=FIELD(R.LC.ACCT.BAL<LCAC.AMT.REC>,@VM,I)
            END
            IF (Y.COMM.CODE EQ 'LCOBTBV.ST') THEN
                Y.COMM.AMT.ST=FIELD(R.LC.ACCT.BAL<LCAC.AMT.REC>,@VM,I)
            END
            IF (Y.COMM.CODE EQ 'LCSWCHGFUL') THEN
                Y.SWIFT.CHRG=FIELD(R.LC.ACCT.BAL<LCAC.AMT.REC>,@VM,I)
            END
            Y.COMM.CODE =''
        NEXT I
        Y.RETURN<-1>=Y.REC.ID:'*':R.ACCT<TF.LC.LC.TYPE>:'*':R.ACCT<TF.LC.OLD.LC.NUMBER>:'*':R.ACCT<TF.LC.LC.AMOUNT>:'*':R.ACCT<TF.LC.LC.AMOUNT.LOCAL>:'*':Y.COMM.AMT:'*':Y.COMM.AMT.ST:'*':Y.SWIFT.CHRG:'*':Y.SWIFT.CHRG*.15:'*':R.REGISTER<BTB.JOB.EX.TF.REF>:'*':R.REGISTER<BTB.JOB.EX.LC.NUMBER>
    REPEAT
    RETURN
END
