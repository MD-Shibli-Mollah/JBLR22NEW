*-----------------------------------------------------------------------------
* <Rating>-1</Rating>
*-----------------------------------------------------------------------------
    SUBROUTINE ABL.S.ACI.INT.CON
    $INSERT GLOBUS.BP I_COMMON
    $INSERT GLOBUS.BP I_EQUATE
    $INSERT JBL.BP I_F.ABL.H.AC.VIOLATION
    $INSERT GLOBUS.BP I_F.GROUP.CREDIT.INT
    $INSERT GLOBUS.BP I_F.GROUP.DATE
    $INSERT GLOBUS.BP I_F.ACCOUNT

**********************************************************************************************
*This Subroutine is used to generate ACI record with zero interest rate according to the     *
*violation in the ABL.H.AC.VIOLATION file.                                                   *
*Last Review Date:28th April,2016                                                            *
**********************************************************************************************

    FN.ABL.VIOL='F.ABL.H.AC.VIOLATION'
    F.ABL.VIOL=''

    FN.GCI='F.GROUP.CREDIT.INT'
    F.GCI =''

    FN.GR.DATE='F.GROUP.DATE'
    F.GR.DATE = ''

    FN.AC='F.ACCOUNT'
    F.AC=''

    CALL OPF(FN.ABL.VIOL,F.ABL.VIOL)
    CALL OPF(FN.GCI,F.GCI)
    CALL OPF(FN.GR.DATE,F.GR.DATE)
    CALL OPF(FN.AC,F.AC)

    Y.GCI.ID = ''
    R.GCI=''
    Y.GR.ID =''
    R.GR.ID = ''
    REC.AC.VIOL =''
    Y.AC.VIOL.ID = ''
    Y.CR.MIN.VALUE = ''
    Y.CR.MIN.WAIVE = ''
    USER.CHOICE =''
    Y.SOURCE= ''
    Y.MESSAGE = ''
    Y.STATUS = ''
    Y.START.DATE = ''
    Y.ACI.ID =''
    Y.CR.MIN.BAL=''
    Y.INT.BASIS=''
    Y.TAX.KEY=''
    Y.BAL.TYPE=''
    Y.BAL.ST.DTE=''
    Y.BAL.ET.DTE=''
    Y.OPEN.AC=''
    Y.CLOSE.AC=''

    Y.START.DATE=TODAY[1,6]

    SEL.CMD.VIOL="SELECT ":FN.ABL.VIOL:" WITH @ID LIKE '....":Y.START.DATE:"'"
    CALL EB.READLIST(SEL.CMD.VIOL,SEL.LIST.VIOL,'',NO.OF.REC.VIOL,SEL.ERR.VIOL)

    CRT" TOTAL RECORDS SELECTED ....... ":NO.OF.REC.VIOL
    CRT" DO YOU WANT TO START THE PROCESSING  Y/N"

    INPUT USER.CHOICE

    IF USER.CHOICE EQ 'Y' THEN
        LOOP
            REMOVE Y.AC.VIOL.ID  FROM SEL.LIST.VIOL SETTING Y.POS
        WHILE Y.AC.VIOL.ID:Y.POS
            CALL F.READ(FN.ABL.VIOL,Y.AC.VIOL.ID,REC.AC.VIOL,F.ABL.VIOL,ERR.ABL.VIOL)
            CALL DBR("ACCOUNT":FM:AC.CONDITION.GROUP,FIELD(Y.AC.VIOL.ID,".",1),Y.GR.ID)
            Y.GR.ID =Y.GR.ID:'BDT'
            CALL F.READ(FN.GR.DATE,Y.GR.ID,R.GR.ID,F.GR.DATE,ERR.GR.DATE)
            Y.GCI.ID=Y.GR.ID:"":R.GR.ID<AC.GRD.CREDIT.GROUP.DATE>

            CALL F.READ(FN.GCI,Y.GCI.ID,R.GCI,F.GCI,ERR.GCI)
            Y.CR.MIN.VALUE=R.GCI<IC.GCI.CR.MIN.VALUE>
            Y.CR.MIN.WAIVE=R.GCI<IC.GCI.CR.MIN.WAIVE>
            Y.INT.BASIS=R.GCI<IC.GCI.INTEREST.DAY.BASIS>
            Y.TAX.KEY=R.GCI<IC.GCI.TAX.KEY>
            Y.BAL.TYPE=R.GCI<IC.GCI.CR.BALANCE.TYPE>
            Y.BAL.ST.DTE=R.GCI<IC.GCI.CR.MIN.BAL.ST.DTE>
            Y.BAL.ET.DTE=R.GCI<IC.GCI.CR.MIN.BAL.ED.DTE>
            Y.OPEN.AC=R.GCI<IC.GCI.CR.ACCR.OPEN.AC>
            Y.CLOSE.AC=R.GCI<IC.GCI.CR.ACCR.CLOSE.AC>
            Y.CR.MIN.BAL=R.GCI<IC.GCI.CR.MINIMUM.BAL>

            Y.ACI.ID =FIELD(Y.AC.VIOL.ID,".",1):"-":Y.START.DATE:"01"
            Y.SOURCE="DM.OFS.SRC"

            Y.MESSAGE="ACCOUNT.CREDIT.INT,INT.ZERO/I/PROCESS,DMUSER.1//":REC.AC.VIOL<ACV.CO.CODE>:",":Y.ACI.ID:",INTEREST.DAY.BASIS=":Y.INT.BASIS:",TAX.KEY=":Y.TAX.KEY:",CR.BALANCE.TYPE=":Y.BAL.TYPE:",CR.MINIMUM.BAL=":Y.CR.MIN.BAL:",CR.INT.RATE=0,CR.MIN.BAL.ST.DTE=":Y.BAL.ST.DTE:",CR.MIN.BAL.ED.DTE=":Y.BAL.ET.DTE:",CR.ACCR.OPEN.AC=":Y.OPEN.AC:",CR.ACCR.CLOSE.AC=":Y.CLOSE.AC:",CR.MIN.VALUE=":Y.CR.MIN.VALUE:",CR.MIN.WAIVE=":Y.CR.MIN.WAIVE
            Y.MSG.ID = ''

            CALL OFS.POST.MESSAGE(Y.MESSAGE,Y.MSG.ID,Y.SOURCE,'')
            CALL JOURNAL.UPDATE('TEST')

            Y.MESSAGE = ''
            IF Y.MSG.ID THEN
                CNT =CNT+1
                CRT" PROCESS................":CNT
                REC.AC.VIOL<ACV.AC.BAL.ACI.ID>=Y.ACI.ID
                WRITE REC.AC.VIOL TO F.ABL.VIOL,Y.AC.VIOL.ID
                REC.AC.VIOL=""
            END
        REPEAT
    END
    RETURN

END 
