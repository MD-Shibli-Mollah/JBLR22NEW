*-----------------------------------------------------------------------------
* <Rating>70</Rating>
*-----------------------------------------------------------------------------
    SUBROUTINE BD.PC.OPEN.VAL.RTN
    $INSERT I_EQUATE
    $INSERT I_COMMON
    $INSERT I_F.LETTER.OF.CREDIT
    $INSERT I_F.LD.LOANS.AND.DEPOSITS
    $INSERT I_F.BD.BTB.JOB.REGISTER

    GOSUB INITIALISE
    GOSUB PROCESS
    RETURN

INITIALISE:

    FN.LD="F.LD.LOANS.AND.DEPOSITS"
    F.LD=""
    CALL OPF(FN.LD,F.LD)

    FN.BD.BTB.JOB.REGISTER = "F.BD.BTB.JOB.REGISTER"
    F.BD.BTB.JOB.REGISTER = ""
    CALL OPF(FN.BD.BTB.JOB.REGISTER,F.BD.BTB.JOB.REGISTER)

    Y.PC.AMT = ''
    Y.EXCH.RATE = ''

    RETURN

PROCESS:
    GOSUB GET.LOC.REF.POS
    IF R.NEW(LD.LOCAL.REF)<1,Y.JOBNO.POS> EQ "" THEN RETURN
    Y.JOB.NO = R.NEW(LD.LOCAL.REF)<1,Y.JOBNO.POS>
    CALL F.READ(FN.BD.BTB.JOB.REGISTER,Y.JOB.NO,R.BTB.JOB.REGISTER,F.BD.BTB.JOB.REGISTER,Y.BTB.JOB.REG.ERR)
    IF R.NEW(LD.LOCAL.REF)<1,Y.PC.FCVAL.POS> EQ "" OR R.NEW(LD.LOCAL.REF)<1,Y.PC.FCVAL.POS> EQ "0" THEN
        AF = LD.LOCAL.REF
        AV = Y.PC.FCVAL.POS
        ETEXT = "PC FC Amount Should Not be Null"
        CALL STORE.END.ERROR
        RETURN
    END
    Y.LD.FC.AMT = R.NEW(LD.LOCAL.REF)<1,Y.PC.FCVAL.POS>
    Y.EXCH.RATE = R.NEW(LD.LOCAL.REF)<1,Y.RATE.POS>
    Y.JOB.PC.ENT.AMT = R.BTB.JOB.REGISTER<BTB.JOB.TOT.PC.AVL.AMT>

    IF Y.LD.FC.AMT GT Y.JOB.PC.ENT.AMT THEN
        Y.PC.DIFF = Y.LD.FC.AMT - Y.JOB.PC.ENT.AMT
        AF = LD.LOCAL.REF
        AV = Y.PC.FCVAL.POS
        ETEXT = "PC Disburse Amt Exceeds Job PC Entitle Amt":Y.PC.DIFF
        CALL STORE.END.ERROR
        RETURN
    END ELSE
        IF Y.EXCH.RATE LE "0" THEN
            Y.PC.AMT = Y.LD.FC.AMT * 1
        END ELSE
            Y.PC.AMT = Y.LD.FC.AMT * Y.EXCH.RATE
        END
        CALL EB.ROUND.AMOUNT('LCCY',Y.PC.AMT,'2','')
        R.NEW(LD.AMOUNT)<1,1> = Y.PC.AMT
    END
    RETURN

GET.LOC.REF.POS:
    CALL GET.LOC.REF("LD.LOANS.AND.DEPOSITS","DOC.VALUE.FC",Y.PC.FCVAL.POS)
    CALL GET.LOC.REF("LD.LOANS.AND.DEPOSITS","EXCHANGE.RATE",Y.RATE.POS)
    CALL GET.LOC.REF("LD.LOANS.AND.DEPOSITS","JOB.NUMBER",Y.JOBNO.POS)
    RETURN
END
