*-----------------------------------------------------------------------------
* <Rating>79</Rating>
*-----------------------------------------------------------------------------
    SUBROUTINE BD.V.SEL.MKT.EXC.RATE
    $INSERT I_COMMON
    $INSERT I_EQUATE
    $INSERT I_F.LETTER.OF.CREDIT
    $INSERT I_F.DRAWINGS
    $INSERT I_F.CURRENCY.MARKET
    $INSERT I_F.CURRENCY
    $INSERT I_F.ACCOUNT
    $INSERT I_GTS.COMMON

!IF OFS.VAL.ONLY THEN RETURN
    GOSUB INIT
    GOSUB PROCESS
    RETURN

INIT:
    FN.CUR = "F.CURRENCY"
    F.CUR = ""
    CALL OPF(FN.CUR,F.CUR)
    R.CUR.REC = ""
    Y.CUR.ERR = ""

    FN.AC = "F.ACCOUNT"
    F.AC = ""
    CALL OPF(FN.AC,F.AC)
    R.AC.REC = ""
    Y.AC.ERR = ""

    Y.BUY.RATE = ''
    Y.SELL.RATE = ''


    Y.APP.VER.NAME = APPLICATION : PGM.VERSION

    IF Y.APP.VER.NAME NE 'DRAWINGS,BD.DISB.FDBP' THEN
        CALL GET.LOC.REF("DRAWINGS","CO.DOC.AMOUNT",Y.DR.DOCAMT.POS)
        R.NEW(TF.DR.LOCAL.REF)<1,Y.DR.DOCAMT.POS> = R.OLD(TF.DR.DOCUMENT.AMOUNT)
    END
    RETURN

PROCESS:
!Y.APP.VER.NAME = APPLICATION : PGM.VERSION
    BEGIN CASE
    CASE Y.APP.VER.NAME EQ 'DRAWINGS,BD.IMPSP' OR Y.APP.VER.NAME EQ 'DRAWINGS,BD.IMPMAT' OR Y.APP.VER.NAME EQ 'DRAWINGS,BD.BTBSP' OR Y.APP.VER.NAME EQ 'DRAWINGS,BD.BTBMAT'
        Y.DR.ID = ID.NEW
        Y.DR.CUR = R.NEW(TF.DR.DRAW.CURRENCY)
        IF Y.DR.CUR NE LCCY THEN
            CALL F.READ(FN.CUR,Y.DR.CUR,R.CUR.REC,F.CUR,Y.CUR.ERR)
            FINDSTR '3' IN R.CUR.REC<EB.CUR.CURRENCY.MARKET> SETTING POS,POS1 THEN
                Y.BUY.RATE = R.CUR.REC<EB.CUR.BUY.RATE,POS1>
                Y.SELL.RATE = R.CUR.REC<EB.CUR.SELL.RATE,POS1>
                CALL F.READ(FN.AC,R.NEW(TF.DR.DRAWDOWN.ACCOUNT),R.AC.REC,F.AC,Y.AC.ERR)
                IF R.AC.REC<AC.CURRENCY> NE Y.DR.CUR AND R.NEW(TF.DR.RATE.BOOKED) EQ "" THEN
                    R.NEW(TF.DR.RATE.BOOKED) = Y.BUY.RATE
                END
                R.AC.REC = ""
                Y.AC.ERR = ""
                CALL F.READ(FN.AC,R.NEW(TF.DR.PAYMENT.ACCOUNT),R.AC.REC,F.AC,Y.AC.ERR)
                IF R.AC.REC<AC.CURRENCY> NE Y.DR.CUR AND R.NEW(TF.DR.TREASURY.RATE) EQ "" THEN
                    R.NEW(TF.DR.TREASURY.RATE) = Y.SELL.RATE
                END
            END
        END
    END CASE
    RETURN
END
