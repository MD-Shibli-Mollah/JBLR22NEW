**************************************************************************
*-----------------------------------------------------------------------------
* <Rating>-131</Rating>
*-----------------------------------------------------------------------------
    SUBROUTINE JBL.AZ.AC.COB.PROCESS(R.EB.BD.EXCISE.DUTY.SLAB,R.EB.BD.EXCISE.DUTY.PARAM,Y.APPLICATION,Y.AC.ID)
**************************************************************************
* Subroutine Description:
*-----------------------
* The Purpose of this Routine is to detect and calculate the excise duty
* for AZ and AC
*-------------------------------------------------------------------------
* Parameter Description
*--------- -----------
* R.EB.BD.EXCISE.DUTY.SLAB       :       It holds the Slab record
* R.EB.BD.EXCISE.DUTY.PARAM      :       It holds the Parameter record
* Y.APPLICATION                  :       It holds the application
* Y.AC.ID                        :       It holds the ID of AZ or AC
*-------------------------------------------------------------------------
*Common Variable Description
*------  ------- -----------
*
*Variable name:              Insert file:           Description:
*
* TODAY                      I_COMMON              Will hold Today's date
*-------------------------------------------------------------------------
*Called Subroutines:
*------ ------------
*Routine Name:             Local/Core:     Description:
*
* EXCISE.DUTY.MAIN.PROCESS     LOCAL        Detects whether the excise
*                                           duty needs to be applied.
* EB.ACCOUNTING                 CORE        Used to generate accounting
*                                           entries
* EB.CURR.CONV                  CORE        Used to convert a currency amt
*                                           to another currency amount
* MIDDLE.RATE.CONV.CHECK        CORE        This routine is used to convert
*                                           the given foreign currency amount
*                                           in to local currency amount(USD) and
*                                           gives the exchange rate and equivalent local amount
*
*-------------------------------------------------------------------------
* Modification History:
* ------------ --------
* 28/02/2011 -                        New   - Rashmitha M
*                                             Thesys Technologies

    $INCLUDE GLOBUS.BP I_EQUATE
    $INCLUDE GLOBUS.BP I_COMMON
    $INCLUDE GLOBUS.BP I_F.AZ.ACCOUNT
    $INCLUDE GLOBUS.BP I_F.ACCOUNT
    $INCLUDE GLOBUS.BP I_F.COMPANY
    $INCLUDE GLOBUS.BP I_F.STMT.ENTRY
    $INCLUDE GLOBUS.BP I_F.DATES
    $INCLUDE GLOBUS.BP I_F.FT.COMMISSION.TYPE
    $INCLUDE JBL.BP I_F.EB.BD.EXCISE.DUTY.PARAM
    $INCLUDE JBL.BP I_F.BD.EXCISE.DUTY.SLAB
    $INCLUDE JBL.BP I_F.EB.BD.EXCISE.DUTY
    $INCLUDE JBL.BP I_F.EB.JBL.AZ.MAT.LIST
    $INCLUDE GLOBUS.BP I_BATCH.FILES
    GOSUB INITIALISE
    GOSUB OPENFILES
    GOSUB PROCESS

    RETURN
*--------------------------------------------------------------------------
PROCESS:
*------
! Check if the Application is AZ or AC
    BEGIN CASE
    CASE Y.APPLICATION EQ 'AZ'
        CALL F.READ(FN.AZ.ACCOUNT,Y.AC.ID,R.AZ.ACCOUNT,F.AZ.ACCOUNT,AZ.ACCOUNT.ERR)
        IF NOT(R.AZ.ACCOUNT) THEN
            RETURN
        END
        Y.CATEGORY = R.AZ.ACCOUNT<AZ.CATEGORY>
        Y.CURRENCY = R.AZ.ACCOUNT<AZ.CURRENCY>
        Y.CUSTOMER = R.AZ.ACCOUNT<AZ.CUSTOMER>
        Y.NOM.ID=R.AZ.ACCOUNT<AZ.NOMINATED.ACCOUNT>
        Y.AZ.MAT.DATE=R.AZ.ACCOUNT<AZ.MATURITY.DATE>
        GOSUB READ.ACCOUNT
        IF Y.OPEN.ACTUAL.BAL EQ "" THEN
            RETURN
        END
        Y.ACCT.OFFICER=R.ACCOUNT<AC.ACCOUNT.OFFICER>
        Y.CURRENCY.MARKET=R.ACCOUNT<AC.CURRENCY.MARKET>
*
    CASE Y.APPLICATION EQ 'AC'
        GOSUB READ.ACCOUNT
        IF NOT(R.ACCOUNT) THEN
            RETURN
        END
        IF Y.OPEN.ACTUAL.BAL EQ "" THEN
            RETURN
        END
        Y.CATEGORY=R.ACCOUNT<AC.CATEGORY>
        Y.CURRENCY=R.ACCOUNT<AC.CURRENCY>
        Y.CUSTOMER=R.ACCOUNT<AC.CUSTOMER>
        Y.ACCT.OFFICER=R.ACCOUNT<AC.ACCOUNT.OFFICER>
        Y.CURRENCY.NMARKET=R.ACCOUNT<AC.CURRENCY.MARKET>
    END CASE
    CALL EXCISE.DUTY.MAIN.PROCESS(R.EB.BD.EXCISE.DUTY.PARAM,Y.CATEGORY,Y.APPLY.CHARGE)
    GOSUB APPLY.EXCISE.DUTY

    RETURN

*-------------------------------------------------------------------------
READ.ACCOUNT:
*************
! Read ACCOUNT
    CALL F.READ(FN.ACCOUNT,Y.AC.ID,R.ACCOUNT,F.ACCOUNT,ACCOUNT.ERR)
    Y.OPEN.ACTUAL.BAL=R.ACCOUNT<AC.OPEN.ACTUAL.BAL>

    RETURN
*-------------------------------------------------------------------------
APPLY.EXCISE.DUTY:
******************
    IF Y.APPLY.CHARGE EQ 'Y' THEN
        GOSUB GET.DUTY.AMT
        GOSUB GEN.ENTRIES

        GOSUB UPDATE.AZ.MAT.LIST
    END

    RETURN
*-------------------------------------------------------------------------
GET.DUTY.AMT:
**************
! Get Excise duty amount
    GOSUB READ.BD.EXCISE.DUTY
    IF NOT(R.EB.BD.EXCISE.DUTY) THEN
        RETURN
    END
    Y.CR.BAL=R.EB.BD.EXCISE.DUTY<EB.BD.46.HIGHEST.CR.BAL.LCY>
    Y.DR.BAL=R.EB.BD.EXCISE.DUTY<EB.BD.46.HIGHEST.DT.BAL.LCY>
    IF Y.CR.BAL GT ABS(Y.DR.BAL) THEN
        Y.BAL=Y.CR.BAL
    END ELSE
        Y.BAL=ABS(Y.DR.BAL)
    END
    Y.AMT=R.EB.BD.EXCISE.DUTY.SLAB<EB.BD.2.AMT.FROM>
    CONVERT VM TO FM IN Y.AMT
    Y.COUNT.AMT=DCOUNT(Y.AMT,FM)
    LOOP
    WHILE AMT.FROM.POS LE Y.COUNT.AMT
        Y.AMT.FRM=R.EB.BD.EXCISE.DUTY.SLAB<EB.BD.2.AMT.FROM,AMT.FROM.POS>
        Y.AMT.TO=R.EB.BD.EXCISE.DUTY.SLAB<EB.BD.2.AMT.TO,AMT.FROM.POS>
        IF Y.AMT.TO EQ '' AND Y.BAL GE Y.AMT.FRM THEN
            Y.SLAB.AMT =R.EB.BD.EXCISE.DUTY.SLAB<EB.BD.2.EXCISE.DUTY.AMT,AMT.FROM.POS>
            EXIT
        END ELSE
            IF Y.BAL GE Y.AMT.FRM AND Y.BAL LE Y.AMT.TO THEN
                Y.SLAB.AMT =R.EB.BD.EXCISE.DUTY.SLAB<EB.BD.2.EXCISE.DUTY.AMT,AMT.FROM.POS>
                EXIT
            END
        END
        AMT.FROM.POS++
    REPEAT
    Y.SLAB.AMT.LCY=Y.SLAB.AMT
    IF Y.CURRENCY NE LCCY THEN
        Y.SLAB.AMT.FCY=''
        CALL MIDDLE.RATE.CONV.CHECK(Y.SLAB.AMT.FCY,Y.CURRENCY,Y.RATE,Y.CURRENCY.MARKET,Y.SLAB.AMT,'','')
    END ELSE
        Y.SLAB.AMT.FCY=''
    END

    RETURN
*-------------------------------------------------------------------------
READ.BD.EXCISE.DUTY:
********************
! Read READ.BD.EXCISE.DUTY
    Y.EB.BD.EXCISE.DUTY.ID=Y.AC.ID:'-':TODAY[1,4]
    CALL F.READ(FN.EB.BD.EXCISE.DUTY,Y.EB.BD.EXCISE.DUTY.ID,R.EB.BD.EXCISE.DUTY,F.EB.BD.EXCISE.DUTY,EB.BD.EXCISE.DUTY.ERR)

    RETURN
*-------------------------------------------------------------------------
GEN.ENTRIES:
*************
! Get internal account category for the company
    LOCATE ID.COMPANY IN R.EB.BD.EXCISE.DUTY.PARAM<EB.BD.30.COMPANY> SETTING COM.POS THEN
        Y.INTERNAL.ACCT.CAT=R.EB.BD.EXCISE.DUTY.PARAM<EB.BD.30.INT.AC.CATEGORY>
    END ELSE
        Y.FIN.COMP=R.COMPANY(EB.COM.FINANCIAL.COM)
        LOCATE Y.FIN.COMP IN R.EB.BD.EXCISE.DUTY.PARAM<EB.BD.30.COMPANY> SETTING COM.POS THEN
            Y.INTERNAL.ACCT.CAT=R.EB.BD.EXCISE.DUTY.PARAM<EB.BD.30.INT.AC.CATEGORY>
        END ELSE
            RETURN
        END
    END
! Form the internal account
    Y.INTERNAL.ACCT=LCCY:Y.INTERNAL.ACCT.CAT:'0001'
    IF C$MULTI.BOOK THEN
        Y.INTERNAL.ACCT:= R.COMPANY(EB.COM.SUB.DIVISION.CODE)
    END
    GOSUB READ.FT.COMMISSION.TYPE
    IF (Y.SLAB.AMT EQ 0) OR NOT(R.FT.COMMISSION.TYPE) THEN
        RETURN
    END

! Statement entry debiting excise duty from the AZ/balance account:
    ENTRY.REC=""
    ENTRY.REC<AC.STE.ACCOUNT.NUMBER>   = Y.AC.ID
    ENTRY.REC<AC.STE.COMPANY.CODE>     = ID.COMPANY
    ENTRY.REC<AC.STE.AMOUNT.LCY>       = -1 * Y.SLAB.AMT.LCY
    ENTRY.REC<AC.STE.AMOUNT.FCY>       = -1*Y.SLAB.AMT.FCY
    ENTRY.REC<AC.STE.TRANSACTION.CODE>=R.FT.COMMISSION.TYPE<FT4.TXN.CODE.DR>
    ENTRY.REC<AC.STE.THEIR.REFERENCE>  = Y.AC.ID
    ENTRY.REC<AC.STE.EXCHANGE.RATE>    = Y.RATE
    ENTRY.REC<AC.STE.NARRATIVE,1> ='Excise Duty'
    ENTRY.REC<AC.STE.CUSTOMER.ID>      = Y.CUSTOMER
    ENTRY.REC<AC.STE.ACCOUNT.OFFICER>  = Y.ACCT.OFFICER
    ENTRY.REC<AC.STE.PRODUCT.CATEGORY> = Y.CATEGORY
    ENTRY.REC<AC.STE.VALUE.DATE>       = TODAY
    ENTRY.REC<AC.STE.CURRENCY>         = Y.CURRENCY
    ENTRY.REC<AC.STE.OUR.REFERENCE>    = Y.AC.ID
    ENTRY.REC<AC.STE.CURRENCY.MARKET>  = Y.CURRENCY.MARKET
    ENTRY.REC<AC.STE.DEPARTMENT.CODE>  = '1'
    ENTRY.REC<AC.STE.EXPOSURE.DATE>    = TODAY
    ENTRY.REC<AC.STE.TRANS.REFERENCE>  = Y.AC.ID
    ENTRY.REC<AC.STE.SYSTEM.ID>        = Y.APPLICATION
    ENTRY.REC<AC.STE.BOOKING.DATE>     = TODAY
    ENTRY.REC<AC.STE.COMPANY.CODE>     = ID.COMPANY
    YENTRY.REC<-1> = LOWER(ENTRY.REC)

! Statement entry crediting excise duty to the internal account
    ENTRY.REC=""
    ENTRY.REC<AC.STE.ACCOUNT.NUMBER>   = Y.INTERNAL.ACCT
    ENTRY.REC<AC.STE.COMPANY.CODE>     = ID.COMPANY
    ENTRY.REC<AC.STE.AMOUNT.LCY>       = Y.SLAB.AMT.LCY
    ENTRY.REC<AC.STE.AMOUNT.FCY>       = Y.SLAB.AMT.FCY
    ENTRY.REC<AC.STE.TRANSACTION.CODE> = R.FT.COMMISSION.TYPE<FT4.TXN.CODE.CR>
    ENTRY.REC<AC.STE.THEIR.REFERENCE>  = Y.AC.ID
    ENTRY.REC<AC.STE.EXCHANGE.RATE>    = ''
    ENTRY.REC<AC.STE.NARRATIVE,1>      = 'Excise Duty'
    ENTRY.REC<AC.STE.CUSTOMER.ID>      = Y.CUSTOMER
    ENTRY.REC<AC.STE.ACCOUNT.OFFICER>  = Y.ACCT.OFFICER
    ENTRY.REC<AC.STE.PRODUCT.CATEGORY> = Y.CATEGORY
    ENTRY.REC<AC.STE.VALUE.DATE>       = TODAY
    ENTRY.REC<AC.STE.CURRENCY>         = Y.CURRENCY
    ENTRY.REC<AC.STE.OUR.REFERENCE>    = Y.AC.ID
    ENTRY.REC<AC.STE.CURRENCY.MARKET>  = Y.CURRENCY.MARKET
    ENTRY.REC<AC.STE.DEPARTMENT.CODE>  = "1"
    ENTRY.REC<AC.STE.EXPOSURE.DATE>    = TODAY
    ENTRY.REC<AC.STE.TRANS.REFERENCE>  = Y.AC.ID
    ENTRY.REC<AC.STE.SYSTEM.ID>        = Y.APPLICATION
    ENTRY.REC<AC.STE.BOOKING.DATE>     = TODAY
    ENTRY.REC<AC.STE.COMPANY.CODE>     = ID.COMPANY
    YENTRY.REC<-1> = LOWER(ENTRY.REC)

    CALL EB.ACCOUNTING(Y.APPLICATION,'SAO',YENTRY.REC,'')
    GOSUB UPD.EB.BD.EXCISE.DUTY

    RETURN
*-------------------------------------------------------------------------
UPD.EB.BD.EXCISE.DUTY:
***********************
! Update EB.BD.EXCISE.DUTY
    R.EB.BD.EXCISE.DUTY<EB.BD.46.EX.DUTY.AMT.LCY>=Y.SLAB.AMT.LCY
    R.EB.BD.EXCISE.DUTY<EB.BD.46.EX.DUTY.AMT.FCY>=Y.SLAB.AMT.FCY
    R.EB.BD.EXCISE.DUTY<EB.BD.46.POSTING.DATE>=TODAY
    CALL F.WRITE(FN.EB.BD.EXCISE.DUTY,Y.EB.BD.EXCISE.DUTY.ID,R.EB.BD.EXCISE.DUTY)

    RETURN
*-------------------------------------------------------------------------
READ.FT.COMMISSION.TYPE:
************************
! Read FT.COMMISSION.TYPE
    Y.COMM.TYPE=R.EB.BD.EXCISE.DUTY.PARAM<EB.BD.30.COM.TYPE.EXCISE>
    CALL F.READ(FN.FT.COMMISSION.TYPE,Y.COMM.TYPE,R.FT.COMMISSION.TYPE,F.FT.COMMISSION.TYPE,FT.COMMISSION.TYPE.ERR)

    RETURN
*-------------------------------------------------------------------------
UPDATE.AZ.MAT.LIST:
*******************
    Y.NEXT.WORK.DAY=R.DATES(EB.DAT.NEXT.WORKING.DAY)
    IF (Y.SLAB.AMT NE '0') AND R.AZ.ACCOUNT AND Y.NOM.ID NE "" AND Y.AZ.MAT.DATE LE Y.NEXT.WORK.DAY THEN
        READ R.EB.JBL.AZ.MAT.LIST FROM F.EB.JBL.AZ.MAT.LIST,Y.NEXT.WORK.DAY ELSE END
        R.EB.JBL.AZ.MAT.LIST<(DCOUNT(R.EB.JBL.AZ.MAT.LIST,FM)+1)>=Y.AC.ID:'*':Y.AZ.MAT.DATE:'*':Y.NOM.ID
        WRITE R.EB.JBL.AZ.MAT.LIST TO F.EB.JBL.AZ.MAT.LIST,Y.NEXT.WORK.DAY
    END
    RETURN
*-------------------------------------------------------------------------
INITIALISE:
*---------
! Initialise
    FN.ACCOUNT.CLASS='F.ACCOUNT.CLASS'
    F.ACCOUNT.CLASS=''
    R.ACCOUNT.CLASS.SAVINGS=''
    ACCOUNT.CLASS.ERR=''
    FN.FT.COMMISSION.TYPE='F.FT.COMMISSION.TYPE'
    F.FT.COMMISSION.TYPE=''
    R.FT.COMMISSION.TYPE=''
    FT.COMMISSION.TYPE.ERR=''
    FN.AZ.ACCOUNT='F.AZ.ACCOUNT'
    F.AZ.ACCOUNT=''
    R.AZ.ACCOUNT=''
    AZ.ACCOUNT.ERR=''
    FN.ACCOUNT='F.ACCOUNT'
    F.ACOUNT=''
    R.ACCOUNT=''
    ACCOUNT.ERR=''
    FN.EB.BD.EXCISE.DUTY='F.EB.BD.EXCISE.DUTY'
    F.EB.BD.EXCISE.DUTY=''
    R.EB.BD.EXCISE.DUTY=''
    EB.BD.EXCISE.DUTY.ERR=''
    FN.EB.JBL.AZ.MAT.LIST='F.EB.JBL.AZ.MAT.LIST'
    F.EB.JBL.AZ.MAT.LIST=''
    COM.POS=''
    AMT.FROM.POS='1'
    SAV.CAT.POS=''
    Y.SLAB.AMT='0'
    Y.BAL='0'
    Y.RATE=''

    RETURN
*--------------------------------------------------------------------------------
OPENFILES:
*********
! Open files
    CALL OPF(FN.EB.BD.EXCISE.DUTY,F.EB.BD.EXCISE.DUTY)
    CALL OPF(FN.FT.COMMISSION.TYPE,F.FT.COMMISSION.TYPE)
    CALL OPF(FN.ACCOUNT.CLASS,F.ACCOUNT.CLASS)
    CALL OPF(FN.AZ.ACCOUNT,F.AZ.ACCOUNT)
    CALL OPF(FN.ACCOUNT,F.ACCOUNT)
    CALL OPF(FN.EB.JBL.AZ.MAT.LIST,F.EB.JBL.AZ.MAT.LIST)

    RETURN
*--------------------------------------------------------------------------------
END
