*-----------------------------------------------------------------------------
* <Rating>-21</Rating>
*-----------------------------------------------------------------------------
    SUBROUTINE ABL.S.FDR.STATEMENT(Y.RETURN)
!    PROGRAM ABL.S.FDR.STATEMENT

    $INSERT I_COMMON
    $INSERT I_EQUATE
    $INSERT I_F.ACCOUNT
    $INSERT I_F.STMT.ENTRY
    $INSERT I_ENQUIRY.COMMON
    $INSERT I_F.TRANSACTION
    $INSERT I_F.AZ.ACCOUNT

    FN.STMT='F.STMT.ENTRY'
    F.STMT=''

    FN.AZ='F.AZ.ACCOUNT'
    F.AZ=''

    FN.AZ.HIS='F.AZ.ACCOUNT$HIS'
    F.AZ.HIS=''

!    Y.FROM.DATE='20101001'
!    Y.TODATE='20110901'

    Y.AZ.ID=ENQ.SELECTION<4,1>
!    Y.AC.ID='0200000127331'
    Y.FROM.DATE=ENQ.SELECTION<4,2>
    Y.TODATE=ENQ.SELECTION<4,3>

!    Y.LIQ.ID='0200000123303'

    Y.COMM.VAL.DT=''
    Y.COMM.BK.DT=''
    Y.COMM.NARR=''
    Y.COMM.MARKER=''
    Y.AC.TR.AMT=''
    Y.AC.RUN.BAL=''
    Y.AC.OPEN.BAL=''
    Y.AC.STMT.LIST=''
    Y.AC.ER=''

    Y.LIQ.TR.AMT=''
    Y.LIQ.RUN.BAL=''
    Y.LIQ.OPEN.BAL=''
    Y.LIQ.STMT.LIST=''
    Y.LIQ.ER=''
    Y.COMM.TOTAL=''

    Y.STMT.ID=''
    REC.STMT=''
    REC.AZ=''
    Y.TR.NARR=''

    Y.CUS =''
    Y.CAT=''
    Y.FDR.PERIOD=''
    Y.PRIN.AMT=''
    Y.FDR.ST.DT=''
    Y.FDR.MT.DT=''
    Y.FDR.RECIPT=''
    Y.FDR.RATE=''

    Y.FDR.PER='FDR.PREIOD'
    Y.FDR.PER.POS=''
    CALL GET.LOC.REF("AZ.ACCOUNT",Y.FDR.PER,Y.FDR.PER.POS)

    Y.FDR.RCP='FDR.RECEIPT.NO'
    Y.FDR.RCP.POS=''
    CALL GET.LOC.REF("AZ.ACCOUNT",Y.FDR.RCP,Y.FDR.RCP.POS)

    CALL OPF(FN.STMT,F.STMT)
    CALL OPF(FN.AZ,F.AZ)
    CALL OPF(FN.AZ.HIS,F.AZ.HIS)

    CALL  F.READ(FN.AZ,Y.AZ.ID,REC.AZ,F.AZ,ERR.AZ)
    IF REC.AZ THEN
    END
    ELSE
        CALL EB.READ.HISTORY.REC(F.AZ.HIS,Y.AZ.ID,REC.AZ,YERROR)
    END
    Y.LIQ.ID=REC.AZ<AZ.REPAY.ACCOUNT>
    Y.CUS =REC.AZ<AZ.CUSTOMER>
    Y.CAT=REC.AZ<AZ.CATEGORY>
    Y.FDR.PERIOD=REC.AZ<AZ.LOCAL.REF,Y.FDR.PER.POS>:"M"
    Y.PRIN.AMT=REC.AZ<AZ.PRINCIPAL>
    Y.FDR.ST.DT=REC.AZ<AZ.VALUE.DATE>
    Y.FDR.MT.DT=REC.AZ<AZ.MATURITY.DATE>
    Y.FDR.RECIPT=REC.AZ<AZ.LOCAL.REF,Y.FDR.RCP.POS>
    Y.FDR.RATE=REC.AZ<AZ.INTEREST.RATE>

    CALL EB.ACCT.ENTRY.LIST(Y.AZ.ID,Y.FROM.DATE,Y.TODATE,Y.AC.STMT.LIST,Y.AC.OPEN.BAL,Y.AC.ER)
    CALL EB.ACCT.ENTRY.LIST(Y.LIQ.ID,Y.FROM.DATE,Y.TODATE,Y.LIQ.STMT.LIST,Y.LIQ.OPEN.BAL,Y.LIQ.ER)

    Y.AC.RUN.BAL=Y.AC.OPEN.BAL
    Y.LIQ.RUN.BAL=Y.LIQ.OPEN.BAL

    IF Y.LIQ.STMT.LIST NE '' THEN
        Y.AC.STMT.LIST=SORT(Y.AC.STMT.LIST:SM:Y.LIQ.STMT.LIST)
    END

    LOOP
        REMOVE Y.STMT.ID FROM Y.AC.STMT.LIST SETTING STMT.POS
    WHILE Y.STMT.ID:STMT.POS

        CALL F.READ(FN.STMT,Y.STMT.ID,REC.STMT,F.STMT,ERR.STMT)
        Y.COMM.VAL.DT=REC.STMT<AC.STE.VALUE.DATE>
        Y.COMM.BK.DT=REC.STMT<AC.STE.BOOKING.DATE>
        Y.COMM.NARR=REC.STMT<AC.STE.TRANS.REFERENCE>
        CHECKFILE1 = 'TRANSACTION': FM:AC.TRA.NARRATIVE: FM: 'L.A'
        CALL DBR(CHECKFILE1,REC.STMT<AC.STE.TRANSACTION.CODE>,Y.TR.NARR)
        Y.COMM.NARR=Y.COMM.NARR:VM:Y.TR.NARR
        IF REC.STMT<AC.STE.THEIR.REFERENCE> NE '' THEN
            Y.COMM.NARR=Y.COMM.NARR:VM:"Cheque ":REC.STMT<AC.STE.THEIR.REFERENCE>
        END

        IF REC.STMT<AC.STE.AMOUNT.LCY> GT 0 THEN
            Y.COMM.MARKER='CR'
        END
        ELSE
            Y.COMM.MARKER='DR'
        END

        IF REC.STMT<AC.STE.ACCOUNT.NUMBER> EQ Y.AZ.ID THEN
            Y.AC.TR.AMT=ABS(REC.STMT<AC.STE.AMOUNT.LCY>)
            Y.AC.RUN.BAL=Y.AC.RUN.BAL + REC.STMT<AC.STE.AMOUNT.LCY>
            Y.LIQ.TR.AMT=''
        END

        ELSE
            Y.LIQ.TR.AMT=ABS(REC.STMT<AC.STE.AMOUNT.LCY>)
            Y.LIQ.RUN.BAL=Y.LIQ.RUN.BAL + REC.STMT<AC.STE.AMOUNT.LCY>
            Y.AC.TR.AMT=''
        END
        Y.COMM.TOTAL=Y.AC.RUN.BAL+Y.LIQ.RUN.BAL
        Y.RETURN<-1>=Y.COMM.VAL.DT:"*":Y.COMM.BK.DT:"*":Y.COMM.NARR:"*":Y.COMM.MARKER:"*":Y.AC.TR.AMT:"*":Y.AC.RUN.BAL:"*":Y.LIQ.TR.AMT:"*":Y.LIQ.RUN.BAL:"*":Y.COMM.TOTAL:"*":Y.CUS:"*":Y.CAT:"*":Y.FDR.PERIOD:"*":Y.PRIN.AMT:"*":Y.FDR.ST.DT:"*":Y.FDR.MT.DT:"*":Y.FDR.RECIPT:"*":Y.FDR.RATE
        Y.TR.NARR=''
    REPEAT

    RETURN
END
