*-----------------------------------------------------------------------------
* <Rating>-32</Rating>
*-----------------------------------------------------------------------------
    SUBROUTINE V.JBL.CCY.DEFAULT.PAIR.OTH
*-----------------------------------------------------------------------------
* Subroutine Description:
*------------------------
* This for Bought and Sold CCY defaulting based on the transaction in First Deal other day.
*
*-----------------------------------------------------------------------------
* Modification History:
* ---------------------
* 24/08/2013 - New - Manjunath Suvarna
*                    Sepit SoftTech Pvt. Ltd.
*-----------------------------------------------------------------------------
    $INSERT I_COMMON
    $INSERT I_EQUATE
    $INSERT I_F.CURRENCY
    $INSERT I_F.FOREX
    $INSERT JBL.BP I_F.BD.FIRST.PAIR.DEAL.DETAILS
*
*
*-----------------------------------------------------------------------------
*
    GOSUB INITIALISE
    GOSUB PROCESS
*
    RETURN
*
*
*-----------------------------------------------------------------------------
INITIALISE:
*
    FN.FOREX = 'F.FOREX'
    F.FOREX = ''
*
    FN.FOREXNAU = 'F.FOREX$NAU'
    F.FOREXNAU = ''
*
    FN.FOREX$HIS = 'F.FOREX$HIS'
    F.FOREX$HIS = ''
*
    FN.FIRST.PAIR = 'F.BD.FIRST.PAIR.DEAL.DETAILS'
    F.FIRST.PAIR = ''
*
    CALL OPF(FN.FOREX,F.FOREX)
    CALL OPF(FN.FOREXNAU,F.FOREXNAU)
    CALL OPF(FN.FOREX$HIS,F.FOREX$HIS)
    CALL OPF(FN.FIRST.PAIR,F.FIRST.PAIR)
*
    RETURN
*
*
*-----------------------------------------------------------------------------
PROCESS:
*
    Y.ID = COMI
*
    CALL F.READ(FN.FOREXNAU,Y.ID,R.FOREXNAU,F.FOREXNAU,Y.ERR)
    IF R.FOREXNAU THEN
        ETEXT = 'EB-FX.UNAUTH.PAIR.DEAL'
        CALL STORE.END.ERROR
    END ELSE
*
*
* History data fetching based on the First Deal id.
        GOSUB HISTORY.LIST

    END
*
    RETURN
*
*
*-----------------------------------------------------------------------------
*
HISTORY.LIST:
* History data fetching based on the First Deal id.
*
    SELECT.CMD = "SELECT ":FN.FIRST.PAIR:" WITH FIRST.DEAL.REF EQ ":Y.ID
    CALL EB.READLIST(SELECT.CMD,SELECT.LIST,"",NO.OF.REC,RETURN.CODE)
    Y.H.ID = SELECT.LIST
    CALL F.READ(FN.FIRST.PAIR,Y.H.ID,R.FIRST.PAIR,F.FIRST.PAIR,Y.ERR)
    Y.CUR.F = R.FIRST.PAIR<BD.FPD.FIRST.CURR.NO>
    Y.HIS.ID = Y.H.ID:";":Y.CUR.F
*
    CALL F.READ(FN.FOREX$HIS,Y.HIS.ID,R.FOREX$HIS,F.FOREX$HIS,Y.ERR)
    Y.BOUGHT.CCY = R.FOREX$HIS<FX.CURRENCY.BOUGHT>
    Y.BOUGHT.AMT = R.FOREX$HIS<FX.AMOUNT.BOUGHT>
    Y.SOLD.CCY = R.FOREX$HIS<FX.CURRENCY.SOLD>
    Y.SOLD.AMT = R.FOREX$HIS<FX.AMOUNT.SOLD>
*
    R.NEW(FX.CURRENCY.BOUGHT) = Y.SOLD.CCY
    R.NEW(FX.CURRENCY.SOLD) = Y.BOUGHT.CCY
    IF Y.SOLD.CCY EQ 'USD' THEN
        R.NEW(FX.AMOUNT.BOUGHT) = Y.SOLD.AMT
    END
    IF Y.BOUGHT.CCY EQ 'USD' THEN
        R.NEW(FX.AMOUNT.SOLD) = Y.BOUGHT.AMT
    END
*
    CALL REFRESH.FIELD(FX.CURRENCY.BOUGHT,'')
    CALL REFRESH.FIELD(FX.CURRENCY.SOLD,'')
    CALL REFRESH.FIELD(FX.AMOUNT.BOUGHT,'')
    CALL REFRESH.FIELD(FX.AMOUNT.SOLD,'')
*
    CALL REBUILD.SCREEN
*
    RETURN
*
*-----------------------------------------------------------------------------
*
END
