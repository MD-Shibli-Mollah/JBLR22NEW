*-----------------------------------------------------------------------------
* <Rating>-35</Rating>
*-----------------------------------------------------------------------------
    SUBROUTINE BD.CAL.JBL.DRAC.COM(Y.DR.ABPCOM.ID,Y.CONTINUE.FLAG,Y.MIN.CHRG.AMT,Y.TOT.CHG.AMT)
    $INSERT GLOBUS.BP I_COMMON
    $INSERT GLOBUS.BP I_EQUATE
    $INSERT GLOBUS.BP I_F.DRAWINGS
    $INSERT GLOBUS.BP I_F.FT.COMMISSION.TYPE
    $INSERT GLOBUS.BP I_F.CURRENCY
    $INSERT GLOBUS.BP I_F.CURRENCY.MARKET
    $INSERT GLOBUS.BP I_GTS.COMMON
    $INSERT GLOBUS.BP I_F.LETTER.OF.CREDIT
    $INSERT GLOBUS.BP I_F.LC.TYPES

    GOSUB INITIALISE
    GOSUB ASSIGN.RATE

    RETURN


*----------
INITIALISE:
*----------

    Y.CHRG.AMT = 0
    Y.LC.TYPE = ''
    Y.IMP.EXP = ''
    Y.BBLC.TYPE = ''
    Y.PROV.PER = ''
    Y.LCOPEN.COM = 0
    Y.LCOPEN.SEQ.COM = 0
    Y.DR.ISSUE.DATE = ''
    Y.DR.EXPIRY.DATE = ''
    Y.TENOR.DAYS = ''
    Y.TOT.CHG.QTR = 0
    Y.TOT.CHG.QTR1 = 0
    Y.TOT.CHG.QTR2 = 0
    Y.LC.AMT = ''
    Y.CAL.CHG.AMT = ''
    Y.TOT.CHG.AMT = ''
    Y.LCY.CHRG.AMT = ''
    Y.CHRG.AMT.WOSTAX = ''
    Y.SOURCE.TAX.AMT = ''


    F.LC.TYPES = ''
    FN.LC.TYPES = 'F.LC.TYPES'
    CALL OPF(FN.LC.TYPES,F.LC.TYPES)
    R.LC.TYPE.REC = ''

    FN.LC = 'F.LETTER.OF.CREDIT'
    F.LC =''
    CALL OPF(FN.LC,F.LC)
    R.LC.REC = ''

    FN.DR = 'F.DRAWINGS'
    F.DR = ''
    CALL OPF(FN.DR,F.DR)
    R.DR.REC = ''

    FN.CUR = 'F.CURRENCY'
    F.CUR = ''
    CALL OPF(FN.CUR,F.CUR)
    R.CUR.REC = ''

    FN.FTCT = 'F.FT.COMMISSION.TYPE'
    F.FTCT = ''
    CALL OPF(FN.FTCT,F.FTCT)
    R.FTCT.REC = ''

    RETURN

*----------
ASSIGN.RATE:
*----------
    Y.CONTINUE.FLAG = ''
    CALL F.READ(FN.FTCT,Y.DR.ABPCOM.ID,R.FTCT.REC,F.FTCT,Y.FTCT.ERR)
    CALL GET.LOC.REF("FT.COMMISSION.TYPE","LC.OPN.SEQ.COM",Y.OPSEQ.COM.POS)
    CALL GET.LOC.REF("LC.TYPES","BB.LCTYPE.CODE",Y.BBLC.NO.POS)
    CALL GET.LOC.REF("DRAWINGS","MATURITY.DATE",Y.MAT.DATE.POS)

    Y.LC.TYPE = R.NEW(TF.DR.LC.CREDIT.TYPE)
    CALL F.READ(FN.LC.TYPES,Y.LC.TYPE,R.LC.TYPE.REC,F.LC.TYPES,ERR.MSG)
    Y.IMP.EXP = R.LC.TYPE.REC<LC.TYP.IMPORT.EXPORT>
    Y.BBLC.TYPE = R.LC.TYPE.REC<LC.TYP.LOCAL.REF,Y.BBLC.NO.POS>
    IF Y.IMP.EXP EQ 'I' AND Y.BBLC.TYPE NE "" THEN          ;! All the calculations in this routine are done only for IMPORT type LCs
        Y.CONTINUE.FLAG = 'Y'
        Y.LCOPEN.COM = R.FTCT.REC<FT4.PERCENTAGE,1,1>
        Y.LCOPEN.SEQ.COM = R.FTCT.REC<FT4.LOCAL.REF,Y.OPSEQ.COM.POS>
        GOSUB COND.CHECK
    END ELSE
        Y.CONTINUE.FLAG = 'N'
        RETURN
    END
    Y.MIN.CHRG.AMT = R.FTCT.REC<FT4.MINIMUM.AMT,1,1>
    RETURN

*----------
COND.CHECK:
*----------
    Y.DR.ISSUE.DATE = R.NEW(TF.DR.VALUE.DATE)
    Y.DR.EXPIRY.DATE = R.NEW(TF.DR.LOCAL.REF)<1,Y.MAT.DATE.POS>
    Y.DAYS = 'C'
    CALL CDD('',Y.DR.ISSUE.DATE,Y.DR.EXPIRY.DATE,Y.DAYS)    ;! Changed tenor
    Y.TENOR.DAYS = Y.DAYS
    Y.TOT.CHG.QTR1 = Y.TENOR.DAYS/90
    Y.TOT.CHG.QTR2 = INT(Y.TOT.CHG.QTR1)
    !IF Y.TOT.CHG.QTR1 GT Y.TOT.CHG.QTR2  THEN
    !    Y.TOT.CHG.QTR = Y.TOT.CHG.QTR2 + 1
    !END ELSE
    !    Y.TOT.CHG.QTR = Y.TOT.CHG.QTR2
    !END
    Y.TOT.CHG.QTR = Y.TOT.CHG.QTR2 + 1
    Y.DR.AMT = R.NEW(TF.DR.DOCUMENT.AMOUNT)
    FOR I=1 TO Y.TOT.CHG.QTR
        IF I EQ "1" THEN
            Y.CAL.CHG.AMT = (Y.DR.AMT * Y.LCOPEN.COM)/100
        END ELSE
            Y.CAL.CHG.AMT = (Y.DR.AMT * Y.LCOPEN.SEQ.COM)/100
        END
        Y.TOT.CHG.AMT = Y.TOT.CHG.AMT + Y.CAL.CHG.AMT
        Y.CAL.CHG.AMT = ''
    NEXT I

    RETURN
END
