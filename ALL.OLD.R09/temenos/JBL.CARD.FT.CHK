*-----------------------------------------------------------------------------
* THIS ROUTINE USE CRMS -CHECK FT BEFORE AUTHRISED THE REQUEST

*Deploy Date: 12 JAN 2019
*-----------------------------------------------------------------------------
    SUBROUTINE JBL.CARD.FT.CHK
!PROGRAM JBL.CARD.FT.CHK

    $INSERT GLOBUS.BP I_COMMON
    $INSERT GLOBUS.BP I_EQUATE
    $INSERT GLOBUS.BP I_F.ACCOUNT
    $INSERT BP I_F.ATM.CARD.MGT
    $INSERT GLOBUS.BP I_F.FUNDS.TRANSFER
    $INSERT BP I_F.JBL.ATM.MAINT.CALC
    $INSERT GLOBUS.BP I_F.FT.COMMISSION.TYPE
    $INCLUDE BP I_F.JBL.CARD.OFF.INFO

    FN.ATM.NAU="F.EB.ATM.CARD.MGT$NAU"
    F.ATM.NAU=""
    FN.JBL.ATM='F.JBL.ATM.MAINT.CALC'
    F.JBL.ATM=''
    FN.FT = "F.FUNDS.TRANSFER"
    F.FT = ""
    FN.FT.NAU = "F.FUNDS.TRANSFER$NAU"
    F.FT.NAU = ""
    FN.FT.HIS = "F.FUNDS.TRANSFER$HIS"
    F.FT.HIS = ""
    FN.COMM="F.FT.COMMISSION.TYPE"
    F.COMM=''
    FN.ACC='F.ACCOUNT'
    F.ACC=''
    FN.CARD.OFF = "F.JBL.CARD.OFF.INFO"
    F.CARD.OFF = ""
    CALL OPF(FN.FT,F.FT)
    CALL OPF(FN.FT.NAU,F.FT.NAU)
    CALL OPF(FN.FT.HIS,F.FT.HIS)
    CALL OPF(FN.JBL.ATM,F.JBL.ATM)
    CALL OPF(FN.ACC,F.ACC)
    CALL OPF(FN.COMM,F.COMM)
    CALL OPF(FN.CARD.OFF,F.CARD.OFF)

    IF V$FUNCTION EQ 'A' AND  R.NEW(EB.ATM19.CARD.STATUS) EQ "PENDING" AND R.NEW(EB.ATM19.ATTRIBUTE5) EQ "" AND R.NEW(EB.ATM19.ISSUE.WAIVE.CHARGE) EQ 'NO'   THEN
        IF R.NEW(EB.ATM19.REQUEST.TYPE) EQ "REISSUE" AND R.NEW(EB.ATM19.REISSUE.REASON) EQ 5 THEN


        END
        ELSE IF R.NEW(EB.ATM19.REQUEST.TYPE) EQ "PINREISSUE" AND R.NEW(EB.ATM19.REISSUE.REASON) EQ 7 THEN


        END
        ELSE
            ETEXT ="PLEASE FIRST DEDUCT ":R.NEW(EB.ATM19.REQUEST.TYPE) :" FEE BY USING ATM Card Related Fee Deduction MENU THIS REQUEST"
            CALL STORE.END.ERROR
        END
    END
    Y.REQUEST.TYPE=R.NEW(EB.ATM19.REQUEST.TYPE)
    Y.AC.ID=R.NEW(EB.ATM19.ACCT.NO)
    CALL F.READ(FN.ACC,Y.AC.ID,REC.ACC,F.ACC,ERR.ACC)
    Y.CATEGORY=REC.ACC<AC.CATEGORY>

    CALL F.READ(FN.CARD.OFF,Y.AC.ID,REC.OPP,F.CARD.OFF,ERR.OPP)
    Y.CARD.OFFER.VAL=REC.OPP<JBL.CARD.OFF.CARD.OFFER>

    CALL F.READ(FN.JBL.ATM,Y.REQUEST.TYPE,REC.ATM.CHRG,F.JBL.ATM,ERR.CDSTD.HF)
    IF REC.ATM.CHRG EQ '' THEN
        ETEXT="There must be a record ": Y.REQUEST.TYPE :" in JBL.ATM.MAINT.CALC"
        CALL STORE.END.ERROR
        RETURN
    END
    Y.INCLUDE.CATEGORY=REC.ATM.CHRG<ATM.MAINT.INCLUDE.CATEGORY>

    FIND Y.CATEGORY IN Y.INCLUDE.CATEGORY SETTING Ap, Vp THEN
        Y.TRN.TYPE=REC.ATM.CHRG<ATM.MAINT.TRANSACTION.TYPE>
        Y.VEN.ACT=REC.ATM.CHRG<ATM.MAINT.VEN.ACCT,Vp>
        Y.VEN.AMT=REC.ATM.CHRG<ATM.MAINT.VEN.CHARGE.AMT,Vp>
        IF Y.CARD.OFFER.VAL NE "50Percent" THEN
            Y.CHRG.TYPE=REC.ATM.CHRG<ATM.MAINT.CHARGE.TYPE,Vp>
            Y.BR.CHRG.AMT=REC.ATM.CHRG<ATM.MAINT.BRANCH.CHARGE.AMT,Vp>
            Y.FT.COMM=REC.ATM.CHRG<ATM.MAINT.FT.COMM.TYPE,Vp>
            Y.FT.COM.AMT=REC.ATM.CHRG<ATM.MAINT.FT.COMM.AMT,Vp>
            Y.VAT.PER=REC.ATM.CHRG<ATM.MAINT.VAT.PERCENT,Vp>
        END
    END
    Y.FT.ID=R.NEW(EB.ATM19.ATTRIBUTE5)

    IF Y.FT.ID  NE "" THEN
        CALL F.READ(FN.FT.NAU,Y.FT.ID,REC.FT.CHK,F.FT.NAU,ERR.FT)

        IF REC.FT.CHK NE "" THEN
            ETEXT ="CRMS-FUNDS TRANSFER UNAUTHRISED STAGE"
            CALL STORE.END.ERROR
        END
        IF REC.FT.CHK EQ "" THEN
            CALL F.READ(FN.FT,Y.FT.ID,REC.FT.CHK,F.FT,ERR.FT.LIVE)
            IF REC.FT.CHK EQ "" THEN
                CALL EB.READ.HISTORY.REC(F.FT.HIS,Y.FT.ID,REC.FT.CHK,Y.ERR.FT.HIS)
                IF REC.FT.CHK<FT.RECORD.STATUS> EQ "REVE" THEN
                    ETEXT ="CRMS-FUNDS TRANSFER ALREADY REVERSED"
                    CALL STORE.END.ERROR
                END
            END
        END


        IF REC.FT.CHK NE "" THEN
            IF Y.TRN.TYPE NE REC.FT.CHK<FT.TRANSACTION.TYPE> THEN
                ETEXT ="CRMS-INVALID TRANSACTION TYPE"
                CALL STORE.END.ERROR
            END

            IF  Y.VEN.ACT NE REC.FT.CHK<FT.CREDIT.ACCT.NO> THEN
                ETEXT ="CRMS-INVALID CREDIT ACCOUNT"
                CALL STORE.END.ERROR
            END
            IF  Y.VEN.AMT NE REC.FT.CHK<FT.DEBIT.AMOUNT> THEN
                ETEXT ="CRMS-INVALID DEBIT AMOUNT"
                CALL STORE.END.ERROR
            END
            IF  Y.AC.ID NE REC.FT.CHK<FT.DEBIT.ACCT.NO> THEN
                ETEXT ="CRMS-INVALID DEBIT ACCOUNT"
                CALL STORE.END.ERROR
            END

            IF Y.FT.COM.AMT NE "" THEN
                IF  Y.FT.COMM NE REC.FT.CHK<FT.COMMISSION.TYPE> THEN
                    ETEXT ="CRMS-INVALID FT COMMISSION"
                    CALL STORE.END.ERROR
                END

                CALL F.READ(FN.COMM,Y.FT.COMM,REC.COMM,F.COMM,ERR.COMM)
                Y.TAX.CODE=REC.COMM<FT4.TAX.CODE>
                CALL EB.ROUND.AMOUNT(LCCY,Y.FT.COM.AMT,"","")


                Y.LIVE.AMT=OCONV(REC.FT.CHK<FT.COMMISSION.AMT>,"MC/A")

                IF   Y.FT.COM.AMT NE Y.LIVE.AMT THEN
                    ETEXT ="CRMS-INVALID FT COMMISSION AMT"
                    CALL STORE.END.ERROR
                END

                Y.TAX.AMT=(Y.FT.COM.AMT*15)/100
                CALL EB.ROUND.AMOUNT(LCCY,Y.TAX.AMT,"","")
                Y.TAX.AMT="BDT":Y.TAX.AMT

                IF Y.TAX.CODE NE "" AND REC.FT.CHK<FT.TAX.AMT> NE Y.TAX.AMT THEN
                    ETEXT ="CRMS-INVALID TAX AMT"
                    CALL STORE.END.ERROR
                END
            END

            IF Y.REQUEST.TYPE EQ "PINREISSUE" OR Y.REQUEST.TYPE EQ "CLOSE" THEN
                IF  Y.FT.COMM NE REC.FT.CHK<FT.COMMISSION.TYPE> THEN
                    ETEXT ="CRMS-INVALID FT COMMISSION"
                    CALL STORE.END.ERROR
                END

                Y.FT.AMT=(REC.FT.CHK<FT.DEBIT.AMOUNT>*15)/100
                CALL EB.ROUND.AMOUNT(LCCY,Y.FT.AMT,"","")
                Y.FT.AMT="BDT":Y.FT.AMT

                IF REC.FT.CHK<FT.COMMISSION.AMT> NE Y.FT.AMT  THEN
                    ETEXT ="CRMS-INVALID COMMISSION AMT"
                    CALL STORE.END.ERROR
                END
            END

        END


    END


    RETURN

