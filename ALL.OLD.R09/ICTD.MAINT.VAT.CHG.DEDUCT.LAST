****************************************************
*-----------------------------------------------------------------------------
* <Rating>339</Rating>
*-----------------------------------------------------------------------------
* PROGRAM     : PROGRAM TO CREATE A CSV FILE OF MAINTANANCE FEE AND VAT DEDUCT ACCOUNT LIST FROM FT APPLICATION
* DEV BY      : MD. IMRAN HASAN
* DEV DATE    : 2016-12-12
* UPDATE DATE : 2017-06-20
* REQ         : ICTD
****************************************************

    PROGRAM ICTD.MAINT.VAT.CHG.DEDUCT.LAST
    $INSERT GLOBUS.BP I_COMMON
    $INSERT GLOBUS.BP I_EQUATE
    $INSERT GLOBUS.BP I_F.ACCOUNT
    $INSERT GLOBUS.BP I_F.FUNDS.TRANSFER
    $INSERT GLOBUS.BP I_F.CATEGORY

    GOSUB INIT
    GOSUB OPENFILES
    GOSUB PROCESS
    GOSUB WRITE.TO.FILE
    RETURN

INIT:

    FN.AC = 'F.ACCOUNT'
    F.AC = ''

    FN.FT = 'F.FUNDS.TRANSFER'
    F.FT = ''

    FN.CAT = 'F.CATEGORY'
    F.CAT = ''

    FN.AC.H = "F.ACCOUNT$HIS"
    F.AC.H = ""
    R.AC.H = ""

!Y.DATA = 'DR.CR.MAKER':'|':'T24.ID':'|':'LEGECY.AC.NO':'|':'ACCOUNT.TITLE':'|':'CATEGORY':'|':'AC.CO.CODE':'|':'TXN.AMOUNT':'|':'TXN.ID':'|':'TXN.CODE':'|':'VALUE.DATE':'|':'CHEQUE.NUMBER':'|':'TXN.CO.CODE':'|':'PERTICULARS':'|':'INPUTER':'|':'MULTI.ID':'|'VAT.AMOUNT':'|'WORKING.BALANCE':'|'AS.ON'

    RETURN

OPENFILES:

    CALL OPF(FN.AC,F.AC)
    CALL OPF(FN.FT,F.FT)
    CALL OPF(FN.CAT,F.CAT)
    CALL OPF(FN.AC.H,F.AC.H)

    CALL GET.LOC.REF("FUNDS.TRANSFER","FT.DR.DETAILS",Y.FT.DR.DETAILS.POS)
    CALL GET.LOC.REF("FUNDS.TRANSFER","FT.CR.DETAILS",Y.FT.CR.DETAILS.POS)

!-------Check Directory----------
    OPEN "RPT.DATA.DIR" TO F.RPT.DATA.DIR
    ELSE
        CMD = "CREATE.FILE RPT.DATA.DIR TYPE=UD"
        EXECUTE CMD
        OPEN "RPT.DATA.DIR" TO F.RPT.DATA.DIR
        ELSE
            CRT "OPENING OF RPT.DATA.DIR FAILED"
        END
    END
!-------------------------------

    Y.FILE.NAME = 'MAINTANANCE.FEE.AND.VAT.DEDUCT.':TODAY:'.csv'
    Y.FILE.DIR = 'RPT.DATA.DIR'
    Y.FILE.PATH = Y.FILE.DIR:'/':Y.FILE.NAME

    RETURN

PROCESS:

    SEL.CMD.FT = 'SELECT ':FN.FT

    CALL EB.READLIST(SEL.CMD.FT,SEL.LIST.FT,"",NO.OF.RECORD.FT,FT.RET.CODE)
    Y.COUNT = 0
    LOOP
        REMOVE Y.REC.ID FROM SEL.LIST.FT SETTING FT.POS
    WHILE Y.REC.ID:FT.POS

        CALL F.READ(FN.FT,Y.REC.ID,R.FT,F.FT,FT.ERR)

        IF R.FT<FT.TRANSACTION.TYPE> EQ 'ACHY' THEN
*********************** Read FT Transaction ****************************

            Y.TXN.AMT=R.FT<FT.DEBIT.AMOUNT>
            Y.TXN.ID=Y.REC.ID
            Y.TXN.CODE=R.FT<FT.TRANSACTION.TYPE>
            Y.VALUE.DATE=R.FT<FT.DEBIT.VALUE.DATE>
            Y.TXN.CO.CODE=R.FT<FT.CO.CODE>
            Y.CHEQUE.NUMBER=R.FT<FT.CHEQUE.NUMBER>

****************** Read Debit Account Information *************************
            Y.DR.T24.ID=R.FT<FT.DEBIT.ACCT.NO>
            Y.DR.PERTICULARS = R.FT<FT.DEBIT.THEIR.REF> : R.FT<FT.LOCAL.REF,Y.FT.DR.DETAILS.POS>
            CALL F.READ(FN.AC, Y.DR.T24.ID, R.DR.AC, F.AC, AC.DR.ERR)
            IF R.DR.AC EQ '' THEN
                CALL F.READ.HISTORY(FN.AC.H,Y.DR.T24.ID,R.DR.H.REC,F.AC.H,YERR)
                Y.DR.LEG.ID=R.DR.H.REC<AC.ALT.ACCT.ID>
                Y.DR.AC.TITLE=R.DR.H.REC<AC.ACCOUNT.TITLE.1>
                Y.DR.CATEGORY=R.DR.H.REC<AC.CATEGORY>
            END
            ELSE
                Y.DR.LEG.ID=R.DR.AC<AC.ALT.ACCT.ID>
                Y.DR.AC.TITLE=R.DR.AC<AC.ACCOUNT.TITLE.1>
                Y.DR.CATEGORY=R.DR.AC<AC.CATEGORY>
            END

            IF LEFT(Y.DR.T24.ID,2) EQ 'PL' THEN
                Y.DR.PL.CAT.ID = SUBSTRINGS(Y.DR.T24.ID,3,5)
                CALL F.READ(FN.CAT,Y.DR.PL.CAT.ID,R.CAT,F.CAT,CAT.ERR)
                Y.DR.AC.TITLE = R.CAT<EB.CAT.DESCRIPTION>
                Y.DR.CATEGORY=Y.DR.PL.CAT.ID
            END

            Y.DR.AC.CO.CODE=R.FT<FT.DEBIT.COMP.CODE>

**************** Read Credit Account Information ****************************
            Y.CR.T24.ID=R.FT<FT.CREDIT.ACCT.NO>
            Y.CR.PERTICULARS = R.FT<FT.CREDIT.THEIR.REF> : R.FT<FT.LOCAL.REF,Y.FT.CR.DETAILS.POS>
            CALL F.READ(FN.AC, Y.CR.T24.ID, R.CR.REC, F.AC, AC.ERR)

            IF R.CR.REC EQ '' THEN
                CALL F.READ.HISTORY(FN.AC.H,Y.CR.T24.ID,R.CR.H.REC,F.AC.H,YERR)
                Y.CR.LEG.ID=R.CR.H.REC<AC.ALT.ACCT.ID>
                Y.CR.AC.TITLE=R.CR.H.REC<AC.ACCOUNT.TITLE.1>
                Y.CR.CATEGORY=R.CR.H.REC<AC.CATEGORY>
            END
            ELSE
                Y.CR.LEG.ID=R.CR.REC<AC.ALT.ACCT.ID>
                Y.CR.AC.TITLE=R.CR.REC<AC.ACCOUNT.TITLE.1>
                Y.CR.CATEGORY=R.CR.REC<AC.CATEGORY>
            END

            IF LEFT(Y.CR.T24.ID,2) EQ 'PL' THEN
                Y.CR.PL.CAT.ID = SUBSTRINGS(Y.CR.T24.ID,3,5)
                CALL F.READ(FN.CAT,Y.CR.PL.CAT.ID,R.CAT,F.CAT,CAT.ERR)
                Y.CR.AC.TITLE = R.CAT<EB.CAT.DESCRIPTION>
                Y.CR.CATEGORY=Y.CR.PL.CAT.ID
            END

            Y.CR.AC.CO.CODE=R.FT<FT.CREDIT.COMP.CODE>

            Y.INPUTER = R.FT<FT.INPUTTER>
            Y.MULTI.ID = R.FT<FT.IN.SWIFT.MSG>

            Y.CHG.AMT = R.FT<FT.LOC.POS.CHGS.AMT>

            Y.DATA<-1> ='DEBIT':'|':Y.DR.T24.ID:'|':Y.DR.LEG.ID:'|':Y.DR.AC.TITLE:'|':Y.DR.CATEGORY:'|':Y.DR.AC.CO.CODE:'|':Y.TXN.AMT:'|':Y.TXN.ID:'|':Y.TXN.CODE:'|':Y.VALUE.DATE:'|':Y.CHEQUE.NUMBER:'|':Y.TXN.CO.CODE :'|':Y.DR.PERTICULARS:'|':Y.INPUTER:'|':Y.MULTI.ID:'|':Y.CHG.AMT:'|':0:'|'
            Y.DATA<-1> ='CREDIT':'|':Y.CR.T24.ID:'|':Y.CR.LEG.ID:'|':Y.CR.AC.TITLE:'|':Y.CR.CATEGORY:'|':Y.CR.AC.CO.CODE:'|':Y.TXN.AMT:'|':Y.TXN.ID:'|':Y.TXN.CODE:'|':Y.VALUE.DATE:'|':Y.CHEQUE.NUMBER:'|':Y.TXN.CO.CODE:'|':Y.CR.PERTICULARS:'|':Y.INPUTER:'|':Y.MULTI.ID:'|':Y.CHG.AMT:'|':0:'|'

        END
        Y.COUNT++
        Y.PROGRESS = MOD(Y.COUNT,5000)
        IF Y.PROGRESS EQ 0 THEN
            Y.PERCENT = Y.COUNT * 100/NO.OF.RECORD.FT
            PRINT DROUND(Y.PERCENT,0):'% is Completed OF Live File.'
        END

    REPEAT

    RETURN

*--------------------------
WRITE.TO.FILE:
*---------------------------

    IF NOT(Y.FILE.NAME) THEN
        CRT "NO FILE IS EXISTED, CREATING........."
    END
    ELSE
        WRITE Y.DATA TO F.RPT.DATA.DIR,Y.FILE.NAME
        CRT "COMPLETE! PLEASE CHECK FILE ":Y.FILE.NAME:" IN THE DIRECTORY ":Y.FILE.DIR
    END

    RETURN
END

