**************************
*Developed By : Mazba.
*Date : 15/11/2018.
*******************
!   PROGRAM ACCOUNT.DR.CR.AMT
    SUBROUTINE (Y.DATA)
    $INSERT GLOBUS.BP I_COMMON
    $INSERT GLOBUS.BP I_EQUATE
    $INSERT GLOBUS.BP I_ENQUIRY.COMMON
    $INSERT GLOBUS.BP I_F.ACCOUNT
    $INSERT GLOBUS.BP I_F.STMT.ENTRY

    GOSUB INIT
    GOSUB OPENFILES
    GOSUB PROCESS
    RETURN

INIT:
    FN.ACCOUNT = 'FBNK.ACCOUNT'
    F.ACCOUNT = ''
    FN.STMT.ENTRY = 'FBNK.STMT.ENTRY'
    F.STMT.ENTRY = ''
    Y.SL.NO = 1

    LOCATE 'CATEGORY' IN ENQ.SELECTION<2,1> SETTING CATEGORY.POS THEN
        Y.CATEGORY =  ENQ.SELECTION<4,CATEGORY.POS>
    END
    LOCATE 'FROM.DATE' IN ENQ.SELECTION<2,1> SETTING FROM.DATE.POS THEN
        Y.FROM.DATE =  ENQ.SELECTION<4,FROM.DATE.POS>
    END

    LOCATE 'TO.DATE' IN ENQ.SELECTION<2,1> SETTING TO.DATE.POS THEN
        Y.TO.DATE =  ENQ.SELECTION<4,TO.DATE.POS>
    END
!   Y.FROM.DATE=20100101
!   Y.TO.DATE =TODAY
!   Y.CATEGORY = '1936'
!   Y.ID.COMPANY = 'BD0010232'
    Y.ERR = ''
    RETURN

OPENFILES:
    CALL OPF(FN.ACCOUNT,F.ACCOUNT)
    CALL OPF(FN.STMT.ENTRY,F.STMT.ENTRY)
    CALL GET.LOC.REF('ACCOUNT','PR.ASSET.CLASS',Y.PR.ASSET.CLASS.POS)
    RETURN

PROCESS:
    SEL.ACC = 'SELECT ':FN.ACCOUNT:' WITH CO.CODE EQ ':ID.COMPANY:' AND CATEGORY EQ ':Y.CATEGORY
    CALL EB.READLIST(SEL.ACC,SEL.LIST,'',NO.OF.REC,RET.CODE)
    LOOP
        REMOVE Y.ACC.ID FROM SEL.LIST SETTING POS
    WHILE Y.ACC.ID:POS
        CALL F.READ(FN.ACCOUNT, Y.ACC.ID , R.ACCOUNT, F.ACCOUNT , Y.ERR)

        Y.TOT.DR.AMT.LCY = 0
        Y.TOT.CR.AMT.LCY = 0
        Y.CATEGORY = R.ACCOUNT<AC.CATEGORY>
        Y.ACC.NAME = R.ACCOUNT<AC.SHORT.TITLE>
        Y.PR.ASSET.CLASS= R.ACCOUNT<AC.LOCAL.REF,Y.PR.ASSET.CLASS.POS>
        BEGIN CASE
        CASE Y.PR.ASSET.CLASS EQ 50
            Y.PR.ASSET.CLASS = 'BL'
        CASE Y.PR.ASSET.CLASS EQ 40
            Y.PR.ASSET.CLASS = 'DF'
        CASE Y.PR.ASSET.CLASS EQ 30
            Y.PR.ASSET.CLASS = 'SS'
        CASE Y.PR.ASSET.CLASS EQ 20
            Y.PR.ASSET.CLASS = 'SMA'
        CASE 1
            Y.PR.ASSET.CLASS = 'UC'
        END CASE
        CALL EB.ACCT.ENTRY.LIST(Y.ACC.ID, Y.FROM.DATE,Y.TO.DATE,Y.AC.STMT.LIST,Y.AC.OPEN.BAL,Y.AC.ER)
        LOOP
            REMOVE Y.STMT.ID FROM Y.AC.STMT.LIST SETTING POS
        WHILE Y.STMT.ID:POS
            CALL F.READ(FN.STMT.ENTRY, Y.STMT.ID, R.STMT, F.STMT.ENTRY, Y.ERR)
            Y.AMOUNT.LCY = R.STMT<AC.STE.AMOUNT.LCY>

            IF Y.AMOUNT.LCY LT 0 THEN
                Y.TOT.DR.AMT.LCY = Y.TOT.DR.AMT.LCY + Y.AMOUNT.LCY
            END
            ELSE
                Y.TOT.CR.AMT.LCY = Y.TOT.CR.AMT.LCY + Y.AMOUNT.LCY
            END
        REPEAT

        Y.DATA<-1> = Y.SL.NO:'*':Y.CATEGORY:'*':Y.ACC.ID:'*':Y.ACC.NAME:'*':Y.TOT.DR.AMT.LCY:'*':Y.TOT.CR.AMT.LCY:'*':Y.PR.ASSET.CLASS:'*':Y.FROM.DATE:'*':Y.TO.DATE
!                       1            2             3             4                  5                   6                     7                  8            9
        Y.SL.NO +=1
    REPEAT
!    PRINT Y.DATA
    RETURN
END
