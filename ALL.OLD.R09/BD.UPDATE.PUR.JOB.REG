*-----------------------------------------------------------------------------
* <Rating>170</Rating>
*-----------------------------------------------------------------------------
    SUBROUTINE BD.UPDATE.PUR.JOB.REG

*-----------------------------------------------------------------------------
* Subroutine Description:
*------------------------
* This auth routine is update BD.BTB.JOB.REGISTER
*-----------------------------------------------------------------------------
* Modification History:
* ---------------------
* 20/04/2012 - New - Rayhan
*
*-----------------------------------------------------------------------------
    $INSERT I_COMMON
    $INSERT I_EQUATE
    $INSERT I_F.LD.LOANS.AND.DEPOSITS
    $INSERT I_F.DRAWINGS
    $INSERT I_F.BD.BTB.JOB.REGISTER

    GOSUB INITIALISE
    GOSUB PROCESS
    GOSUB UPDATE.DR.PUR.ID

    RETURN
*-----------------------------------------------------------------------------
INITIALISE:
***********
    FN.LD.LOANS.AND.DEPOSITS = 'F.LD.LOANS.AND.DEPOSITS'
    F.LD.LOANS.AND.DEPOSITS  = ''
    CALL OPF(FN.LD.LOANS.AND.DEPOSITS,F.LD.LOANS.AND.DEPOSITS)
    R.LD.LOANS.AND.DEPOSITS  = ''

    FN.DRAWINGS = 'F.DRAWINGS'
    F.DRAWINGS = ''
    CALL OPF(FN.DRAWINGS,F.DRAWINGS)
    R.DRAWINGS = ''
    Y.DRAWINGS.ERR = ''

    FN.BD.BTB.JOB.REGISTER = 'F.BD.BTB.JOB.REGISTER'
    F.BD.BTB.JOB.REGISTER = ''
    CALL OPF(FN.BD.BTB.JOB.REGISTER,F.BD.BTB.JOB.REGISTER)
    R.BD.BTB.JOB.REGISTER = ''
    RETURN
*-----------------------------------------------------------------------------
PROCESS:
********
    CALL GET.LOC.REF('LD.LOANS.AND.DEPOSITS','LINKED.TFDR.REF',Y.LD.DRNO.POS)
    CALL GET.LOC.REF("LD.LOANS.AND.DEPOSITS","JOB.NUMBER",Y.LD.JOB.NO.POS)
    CALL GET.LOC.REF('LD.LOANS.AND.DEPOSITS','BILL.DOC.VAL',Y.LD.DOC.VAL.POS)
    CALL GET.LOC.REF("LD.LOANS.AND.DEPOSITS","PURCHASE.FC.AMT",Y.LD.PUR.AMT.POS)
    CALL GET.LOC.REF("LD.LOANS.AND.DEPOSITS","EXCHANGE.RATE",Y.LD.XRATE.POS)
    CALL GET.LOC.REF("LD.LOANS.AND.DEPOSITS","LIMIT.PROD",Y.LD.LI.PROD.POS)
    CALL GET.LOC.REF("DRAWINGS","DOC.TYPE",Y.DR.DOC.TYPE.POS)

    Y.JOB.NO =  R.NEW(LD.LOCAL.REF)<1,Y.LD.JOB.NO.POS>
    Y.PUR.FCY.AMT = R.NEW(LD.LOCAL.REF)<1,Y.LD.PUR.AMT.POS>[4,LEN(R.NEW(LD.LOCAL.REF)<1,Y.LD.PUR.AMT.POS>)]
    Y.LD.ID =ID.NEW
    Y.DR.ID = R.NEW(LD.LOCAL.REF)<1,Y.LD.DRNO.POS>
    Y.LC.ID = Y.DR.ID[1,12]
    IF Y.JOB.NO EQ '' THEN RETURN
    CALL F.READ(FN.BD.BTB.JOB.REGISTER,Y.JOB.NO,R.BTB.JOB.REGISTER,F.BD.BTB.JOB.REGISTER,R.BTB.JOB.REG.ERR)
    Y.EX.TF.NO = R.BTB.JOB.REGISTER<BTB.JOB.EX.TF.REF>
    LOCATE Y.LC.ID IN Y.EX.TF.NO<1,1> SETTING Y.COUNT.POS THEN
        Y.COUNT = Y.COUNT.POS
        Y.EX.DR.NO = R.BTB.JOB.REGISTER<BTB.JOB.EX.DR.ID,Y.COUNT>
        LOCATE Y.DR.ID IN Y.EX.DR.NO<1,1> SETTING Y.DR.CNT.POS THEN
            Y.DR.CNT=Y.DR.CNT.POS
            R.BTB.JOB.REGISTER<BTB.JOB.EX.PUR.ID,Y.COUNT,Y.DR.CNT>=Y.LD.ID
            R.BTB.JOB.REGISTER<BTB.JOB.EX.PUR.LI.PROD,Y.COUNT,Y.DR.CNT>=R.NEW(LD.LOCAL.REF)<1,Y.LD.LI.PROD.POS>
            R.BTB.JOB.REGISTER<BTB.JOB.EX.PUR.STAT.DT,Y.COUNT,Y.DR.CNT>=R.NEW(LD.VALUE.DATE)
            R.BTB.JOB.REGISTER<BTB.JOB.EX.PUR.MAT.DT,Y.COUNT,Y.DR.CNT>=R.NEW(LD.FIN.MAT.DATE)
            R.BTB.JOB.REGISTER<BTB.JOB.EX.PUR.FC.CUR,Y.COUNT,Y.DR.CNT>=R.NEW(LD.LOCAL.REF)<1,Y.LD.PUR.AMT.POS>[1,3]
            R.BTB.JOB.REGISTER<BTB.JOB.EX.PUR.AMT.FCY,Y.COUNT,Y.DR.CNT>=Y.PUR.FCY.AMT
            R.BTB.JOB.REGISTER<BTB.JOB.EX.PUR.EX.RT,Y.COUNT,Y.DR.CNT>=R.NEW(LD.LOCAL.REF)<1,Y.LD.XRATE.POS>
            R.BTB.JOB.REGISTER<BTB.JOB.EX.PUR.AMT.LCY,Y.COUNT,Y.DR.CNT>=R.NEW(LD.AMOUNT)
            CALL F.WRITE(FN.BD.BTB.JOB.REGISTER,Y.JOB.NO,R.BTB.JOB.REGISTER)
        END
    END
    RETURN

UPDATE.DR.PUR.ID:
****************
    CALL F.READ(FN.DRAWINGS,Y.DR.ID,R.DRAWINGS,F.DRAWINGS,Y.DR.ERR)
    IF R.DRAWINGS EQ '' THEN RETURN
    R.DRAWINGS<TF.DR.LOCAL.REF,Y.DR.DOC.TYPE.POS> = "PURCHASE"
    CALL F.WRITE(FN.DRAWINGS,Y.DR.ID,R.DRAWINGS)
    RETURN
END
