    SUBROUTINE LN.ONE.MONTH.EX(Y.DATA)
    $INSERT GLOBUS.BP I_COMMON
    $INSERT GLOBUS.BP I_EQUATE
    $INSERT GLOBUS.BP I_F.ACCOUNT
    $INSERT GLOBUS.BP I_F.LIMIT
    $INSERT GLOBUS.BP I_F.LN.ASSET.CLASS
    $INSERT GLOBUS.BP I_ENQUIRY.COMMON

    GOSUB INITIALIZE
    GOSUB OPENFILE
    GOSUB PROCESS
    RETURN

INITIALIZE:

    FN.ACC = 'FBNK.ACCOUNT'
    F.ACC = ''
    FN.LIMIT = 'FBNK.LIMIT'
    F.LIMIT = ''
    FN.LN.CLS='F.LN.ASSET.CLASS'
    F.LN.CLS=''
    Y.APPLICATION = "ACCOUNT"
    FIELD.NAME = 'LOAN.MAT.DATE':VM:'LOAN.START.DATE':VM:'PR.ASSET.CLASS'
    FIELD.POS = ''
    CALL MULTI.GET.LOC.REF(Y.APPLICATION,FIELD.NAME,FIELD.POS)
    Y.LOAN.MAT.DATE.POS=FIELD.POS<1,1>
    Y.LOAN.START.DATE.POS=FIELD.POS<1,2>
    Y.PR.ASSET.CLASS.POS=FIELD.POS<1,3>
    RETURN

OPENFILE:
    CALL OPF(FN.ACC, F.ACC)
    CALL OPF(FN.LIMIT, F.LIMIT)
    CALL OPF(FN.LN.CLS,F.LN.CLS)
    RETURN

PROCESS:

    Y.CATEG.ID = ENQ.SELECTION<4,1>
    Y.FROM.DATE=ENQ.SELECTION<4,2>
    Y.TO.DATE=ENQ.SELECTION<4,3>
    IF UPCASE(Y.CATEG.ID) EQ 'ALL' THEN
        SEL.CMD = 'SELECT FBNK.ACCOUNT WITH CO.CODE EQ ':ID.COMPANY
    END ELSE
        SEL.CMD = 'SELECT FBNK.ACCOUNT WITH CATEGORY EQ ':Y.CATEG.ID:' AND CO.CODE EQ ':ID.COMPANY
    END
    CALL EB.READLIST(SEL.CMD, SEL.LIST, "", NO.OF.RECORD, RETURNN.CODE)
    LOOP
        REMOVE Y.ACC.ID FROM SEL.LIST SETTING POS
    WHILE Y.ACC.ID:POS
        CALL F.READ(FN.ACC, Y.ACC.ID, R.ACC, F.ACC, Y.ACC.ERR)
        Y.SHORT.TITLE = R.ACC<AC.SHORT.TITLE>
        Y.LIMIT.REF = R.ACC<AC.LIMIT.REF>
        Y.WORKING.BALANCE = R.ACC<AC.WORKING.BALANCE>
        Y.PR.ASSET.CLASS = R.ACC <AC.LOCAL.REF, Y.PR.ASSET.CLASS.POS>
        IF Y.PR.ASSET.CLASS<>'' THEN
            CALL F.READ(FN.LN.CLS,Y.PR.ASSET.CLASS,R.LN.CLS,F.LN.CLS,CLS.ERR)
            Y.STATUS = R.LN.CLS<LN.ASSCLS.DESCRIPTION>
        END ELSE
            Y.STATUS='Standard'
        END
        Y.CUSTOMER = R.ACC<AC.CUSTOMER>
        IF Y.LIMIT.REF NE '' THEN
            Y.LIMIT.ID = Y.CUSTOMER:'.000':Y.LIMIT.REF
            CALL F.READ(FN.LIMIT, Y.LIMIT.ID, R.LIMIT, F.LIMIT, Y.LIMIT.ERR)
            Y.LOAN.MAT.DATE = R.LIMIT<LI.EXPIRY.DATE>
            Y.LOAN.STRAT.DATE=R.ACC<AC.LOCAL.REF, Y.LOAN.START.DATE.POS>
        END ELSE
            Y.LOAN.MAT.DATE = R.ACC<AC.LOCAL.REF, Y.LOAN.MAT.DATE.POS>
            Y.LOAN.STRAT.DATE=R.ACC<AC.LOCAL.REF, Y.LOAN.START.DATE.POS>
        END
        IF Y.LOAN.MAT.DATE GE Y.FROM.DATE AND Y.LOAN.MAT.DATE LE Y.TO.DATE  THEN
            IF Y.WORKING.BALANCE EQ '' THEN
                Y.WORKING.BALANCE='0.0'
                Y.DATA<-1> = Y.ACC.ID:'*':Y.SHORT.TITLE:'*':Y.LOAN.STRAT.DATE:'*':Y.LOAN.MAT.DATE:'*':Y.WORKING.BALANCE:'*':Y.STATUS
            END ELSE
                Y.DATA<-1> = Y.ACC.ID:'*':Y.SHORT.TITLE:'*':Y.LOAN.STRAT.DATE:'*':Y.LOAN.MAT.DATE:'*':Y.WORKING.BALANCE:'*':Y.STATUS
            END
        END
    REPEAT
    RETURN
END
